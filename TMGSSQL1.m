TMGSSQL1 ;TMG/ELH - Initiate the SQL queries From SequelPMS; 12/19/24
        ;;1.0;TMG-LIB;**1**; 12/19/24
        ;"
 ;"TMG SEQUEL SQL FUNCTIONS 
 ;
 ;"~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--
 ;"Copyright (c) 6/23/2015  Kevin S. Toppenberg MD
 ;"
 ;"This file is part of the TMG LIBRARY, and may only be used in accordence
 ;" to license terms outlined in separate file TMGLICNS.m, which should 
 ;" always be distributed with this file.
 ;"~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--
 ;"
 ;"=======================================================================
 ;"  NOTES ON FUNCTIONALITY:
 ;"  Overview - SequelMed's Oracle database utilizes a program called UViews, which allows
 ;"    connections for SQL Queries. To use this, you must have a PC with Oracle's
 ;"    11g 64-bit client installed. I have written a utility that will initiate
 ;"    the connection and make the calls.
 ;"   Details - For the initial project, the program polls a directory (V:\SQL) for 
 ;"    a file named SQLRequest.txt. The contents of this file should contain:
 ;"    the SQL Query statement^OutputFilePathName. When the program finds a file in the folder
 ;"    it will create a text file named SQLRunning.txt to alert any other processes that
 ;"    it is currently busy. Once the process is complete this file will be removed.
 ;"    
 ;"
 ;"=======================================================================
 ;" API -- Public Functions.
 ;"=======================================================================
 ;"
 ;"SQLREQUEST(SQLSTATEMENT,SQLOUTPATH,SQLOUTNAME,DEBUGMODE)  ;"
 ;"      Purpose: This will be the main function to submit the SQL statement and watch for the 
 ;"                process to finish.
 ;"GTSCHSTA()  ;""   GET CURRENT SCHEDULE STATUS
 ;"GTFTRSCH()  ;""   GET FUTURE APPOINTMENTS
 ;"GTPTDEMO()  ;""   GET PATIENT DEMOGRAPHICS
 ;"GTPTENC()   ;""   GET PATIENT ENCOUNTER INFO (CPTS AND ICDS)  
 ;"LOGLINE(LOGFILE,LINE,CLEARFILE) ;"" ADD NEW LINE TO LOG FILE
 ;"
 ;"
 ;"=======================================================================
 ;" CONSTANTS
 ;"=======================================================================
 ;"
 ;"SQLDIRECTORY()    ;""Directory for SQL communications 
 ;"WINDIR()          ;""Directory for the Windows save (should map directly to SQLDIRECTORY
 ;"LOGDIRECTORY()    ;""Directory for SQL communications  
 ;"SQLRUNNINGFILE()  ;""Name of SQL running file 
 ;"SQLREQUESTFILE()  ;""Name of SQL request file name 
 ;"DATAFILETYPE()    ;""Extension for data file  
 ;"LOGFILETYPE()     ;""Extension for log file 
 ;"SCHSTATF()        ;""File name for schedule status 
 ;"FUTSCHDF()        ;""File name for future appointments 
 ;"PTDEMOF()         ;""File name for patient demographics 
 ;"ENCOUNTF()        ;""File name for encounter information
 ;"SQLSTAT()         ;""SQL Statement for schedule status 
 ;"SQLAPPT()         ;""SQL Statement for future appointments 
 ;"SQLDEMO()         ;""SQL Statement for patient demographics 
 ;"SQLENC()          ;""SQL Statement for encounter information
 ;" 
 ;"=======================================================================
 ;" Private Functions.
 ;"=======================================================================
 ;"FILEWAIT(FILEPATHNAME,TIMEOUTMINS,DEBUGMODE,FORCEREMOVE)  
 ;"   Purpose: This function is used to ensure a file doesn't exists, or if it does
 ;"         wait for it to be removed.
 ;"         It will return a 1 if the file isn't there or is removed in the 
 ;"         appropriate amount of time (TIMEOUTMINS)
 ;"OPENCHCK()  ;" 
 ;" This function will determine if a process is running during a time our office is considered open
 ;"
 ;"
 ;"==================================================================================================
 ;"==========================   SQL CONSTANTS BELOW. ANY PROCESSES    ===============================
 ;"==========================  ANY FUNCTIONS SHOULD REFERENCE THESE   ===============================
 ;"================================================================================================== 
SQLDIRECTORY()  ;"Directory for SQL communications
 QUIT "/mnt/WinServer/SQL/"
 ;"
WINDIR()   ;"Directory for the Windows save (should map directly to SQLDIRECTORY
 QUIT "V:\SQL\"  
 ;"
LOGDIRECTORY()  ;"Directory for SQL communications
 NEW LOGDIR SET LOGDIR="/mnt/WinServer/SQL/LOGS/"
 NEW DATE SET DATE=$P($$HTE^XLFDT($horolog,"5Z"),"@",1)
 NEW YYYY,MM,DD SET YYYY=$P(DATE,"/",3),MM=$P(DATE,"/",1),DD=$P(DATE,"/",2)
 SET LOGDIR=LOGDIR_YYYY_"/"_MM_"/"_DD_"/"
 IF $$EnsureDir^TMGKERNL(LOGDIR)=0 SET LOGDIR="/mnt/WinServer/SQL/LOGS/"  ;"COULDN'T CREATE SO LEAVE AS ROOT
 QUIT LOGDIR
 ;" 
SQLRUNNINGFILE()  ;"Name of SQL running file
 QUIT "SQLRunning.txt"
 ;"
SQLREQUESTFILE()  ;"Name of SQL request file name
 QUIT "SQLRequest.txt"
 ;"
DATAFILETYPE()  ;"Extension for data file
 QUIT ".csv"
 ;" 
LOGFILETYPE()  ;"Extension for log file
 QUIT ".log"
 ;"
SCHSTATF()  ;"File name for schedule status
 QUIT "SQLSchStatus"
 ;"
FUTSCHDF()  ;"File name for future appointments
 QUIT "FutureAppointments"
 ;"
PTDEMOF()  ;"File name for patient demographics
 QUIT "Demographics"
 ;"
ENCOUNTF()  ;"File name for encounter information
 QUIT "EncounterData"
 ;"
RANDOMF()   ;"File name for Random SQL calls
 QUIT "Random"_$J_"Data"
 ;"
APPTCALLF()  ;"File name for schedule status
 QUIT "ApptCalls"
 ;" 
PAFF()  ;"File name for PAFs
 QUIT "PAFS"
 ;"  
GT3XS()  ;"File name for submissions > 3 times
 QUIT "GT3XS"
 ;"   
PHQ9F()  ;"File name for PHQ-9
 QUIT "PHQ9"
 ;"
SQLSTAT()  ;"SQL Statement for schedule status
 QUIT "SELECT a.PROVIDER,a.ACCOUNT_NUM,a.APPOINTMENT_DATE,a.REASON,s.DURATION,a.CHECKIN_DATE,a.CHECKOUT_DATE,a.APPOITMENT_STATUS,a.CHECKIN_BY FROM U_PATIENT_APPOINTMENTS a JOIN U_SLOTS s ON a.SLOT_SEQ_NUM = s.SLOT_SEQ_NUM WHERE TRUNC(a.APPOINTMENT_DATE) = TRUNC(SYSDATE) AND a.APPOITMENT_STATUS = 'SCHEDULED'"
 ;"
SQLAPPT()  ;"SQL Statement for future appointments
 QUIT "SELECT a.PROVIDER,a.ACCOUNT_NUM,a.PATIENT_LAST_NAME,a.PATIENT_FIRST_NAME,d.DOB,a.APPOINTMENT_DATE,a.REASON,s.DURATION,a.COMMENTS,a.APPOITMENT_STATUS,a.CHECKIN_BY FROM U_PATIENT_APPOINTMENTS a JOIN U_SLOTS s ON a.SLOT_SEQ_NUM = s.SLOT_SEQ_NUM JOIN U_PATIENT_DEMOGRAPHIC d ON a.PATIENT_SEQ_NUM = d.PATIENT_SEQ_NUM WHERE TRUNC(a.APPOINTMENT_DATE) > TRUNC(SYSDATE) AND a.APPOITMENT_STATUS = 'SCHEDULED'"
 ;"
SQLDEMO()  ;"SQL Statement for patient demographics
 QUIT "SELECT pd.PATIENT_SEQ_NUM, pd.PATIENT_PRACTICE, pd.LAST_NAME AS PATIENT_LAST_NAME, pd.FIRST_NAME AS PATIENT_FIRST_NAME, pd.ACCOUNT_NUM, pd.ADDRESS1, pd.STATE, prp.LAST_NAME AS RESP_PARTY_LAST_NAME, prp.FIRST_NAME AS RESP_PARTY_FIRST_NAME, pd.PRACTICE_SEQ_NUM, pd.ADDRESS2, pd.PATIENT_LOCATION, pd.CITY, pd.PATIENT_PROVIDER, pd.ZIPCODE, pd.CLASS_SEQ_NUM, pd.DOB, pd.HOME_TEL, pd.CELL_NUM, pd.SSN, pd.SEX, pd.ACTIVE, (SELECT RTRIM(XMLAGG(XMLELEMENT(e, pp.PLAN || '|' || pp.PLAN_ORDER || '|' || pp.ID_NUM || '|' || pp.GROUP_NAME || '|' || pp.ACTIVE_PLAN || ', ') ORDER BY pp.PLAN).EXTRACT('//text()'), ', ') FROM U_PATIENT_PLANS pp WHERE pp.PATIENT_SEQ_NUM = pd.PATIENT_SEQ_NUM AND pp.ACTIVE_PLAN = 'Y') AS INSURANCES, pd.COMMENTS FROM U_PATIENT_DEMOGRAPHIC pd LEFT JOIN U_PATIENT_RESP_PARTY prp ON pd.PATIENT_RESP_SEQ_NUM = prp.PATIENT_RESP_SEQ_NUM WHERE pd.PATIENT_PRACTICE = 'FPG'"
 ;"QUIT "SELECT pd.PATIENT_SEQ_NUM, pd.PATIENT_PRACTICE, pd.LAST_NAME AS PATIENT_LAST_NAME, pd.FIRST_NAME AS PATIENT_FIRST_NAME, pd.ACCOUNT_NUM, pd.ADDRESS1, pd.STATE, prp.LAST_NAME AS RESP_PARTY_LAST_NAME, prp.FIRST_NAME AS RESP_PARTY_FIRST_NAME, pd.PRACTICE_SEQ_NUM, pd.ADDRESS2, pd.PATIENT_LOCATION, pd.CITY, pd.PATIENT_PROVIDER, pd.ZIPCODE, pd.CLASS_SEQ_NUM, pd.DOB, pd.HOME_TEL, pd.CELL_NUM, pd.SSN, pd.SEX, pd.ACTIVE, (SELECT RTRIM(XMLAGG(XMLELEMENT(e, pp.PLAN || '|' || pp.PLAN_ORDER || '|' || pp.ID_NUM || '|' || pp.GROUP_NAME || '|' || pp.ACTIVE_PLAN || ', ') ORDER BY pp.PLAN).EXTRACT('//text()'), ', '), pd.COMMENTS FROM U_PATIENT_PLANS pp WHERE pp.PATIENT_SEQ_NUM = pd.PATIENT_SEQ_NUM AND pp.ACTIVE_PLAN = 'Y') AS INSURANCES FROM U_PATIENT_DEMOGRAPHIC pd LEFT JOIN U_PATIENT_RESP_PARTY prp ON pd.PATIENT_RESP_SEQ_NUM = prp.PATIENT_RESP_SEQ_NUM WHERE pd.PATIENT_PRACTICE = 'FPG'"
 ;"
SQLENC()   ;"SQL Statement for encounter information
 QUIT "SELECT pd.PATIENT_SEQ_NUM,pd.PATIENT_PRACTICE,pd.LAST_NAME,pd.FIRST_NAME,pd.ACCOUNT_NUM,pd.DOB,c.VISIT_DATE_FROM,c.CPT_CODE,c.ICD_9_CODE1,c.ICD_9_CODE2,c.ICD_9_CODE3,c.ICD_9_CODE4,c.CHARGE_PROVIDER,c.ENTERED_BY  FROM U_PATIENT_DEMOGRAPHIC pd JOIN U_CHARGES c ON pd.PATIENT_SEQ_NUM = c.PATIENT_SEQ_NUM WHERE c.VISIT_DATE_FROM >= TRUNC(SYSDATE) - 14 AND c.VISIT_DATE_FROM < TRUNC(SYSDATE)"
 ;"
SQLFORMS(DATE)  ;"SQL Statement for the Scanner Barcoded forms
 QUIT "SELECT a.PROVIDER,a.APPOINTMENT_DATE,'""' || a.PATIENT_LAST_NAME || ',' || a.PATIENT_FIRST_NAME || '""' AS PatientName,a.APPOINTMENT_DATE,s.DURATION,a.REASON,a.PATIENT_LAST_NAME,a.PATIENT_FIRST_NAME,a.ACCOUNT_NUM,a.ACCOUNT_NUM,d.DOB,d.HOME_TEL,a.COMMENTS,a.CHECKIN_BY,a.CHECKIN_BY,a.CHECKIN_BY,a.CHECKIN_BY,a.CHECKIN_BY,a.CHECKIN_BY FROM U_PATIENT_APPOINTMENTS a JOIN U_SLOTS s ON a.SLOT_SEQ_NUM = s.SLOT_SEQ_NUM JOIN U_PATIENT_DEMOGRAPHIC d ON a.PATIENT_SEQ_NUM = d.PATIENT_SEQ_NUM WHERE TRUNC(a.APPOINTMENT_DATE) = DATE '"_DATE_"' AND a.APPOITMENT_STATUS = 'SCHEDULED' ORDER BY a.PROVIDER ASC, a.APPOINTMENT_DATE ASC"
 ;"
BITEMSQL(DATE)  ;"SQL Statement for getting the charges for a given date
 QUIT "SELECT pd.ACCOUNT_NUM,c.VISIT_DATE_FROM,c.CPT_CODE,c.ICD_9_CODE1,c.ICD_9_CODE2,c.ICD_9_CODE3,c.ICD_9_CODE4 FROM U_CHARGES c JOIN U_PATIENT_DEMOGRAPHIC pd ON c.PATIENT_SEQ_NUM = pd.PATIENT_SEQ_NUM WHERE c.VISIT_DATE_FROM = TO_DATE('"_DATE_"','MM/DD/YYYY')"
 ;"
FBUCKSQL()  ;"SQL Statement for getting Followup Bucket items
 QUIT "SELECT fb.VISIT_SEQ_NUM, fb.SUSPENDED, pd.ACCOUNT_NUM, pd.LAST_NAME, pd.FIRST_NAME, pd.DOB, c.VISIT_DATE_FROM, c.CPT_CODE, c.MODIFIER_CODE1, c.MODIFIER_CODE2,  c.PLAN_BALANCE FROM U_FOLLOWUP_BUCKET fb JOIN U_CHARGES c ON fb.VISIT_SEQ_NUM = c.VISIT_SEQ_NUM JOIN U_PATIENT_DEMOGRAPHIC pd ON c.PATIENT_SEQ_NUM = pd.PATIENT_SEQ_NUM WHERE c.PLAN_BALANCE > 0"
 ;"
FUSUBMIT()  ;"SQL Statement for getting Followup Bucket Items submitted more than 4 times
 ;"QUIT "SELECT fb.VISIT_SEQ_NUM, fb.SUSPENDED, pd.ACCOUNT_NUM, pd.LAST_NAME, pd.FIRST_NAME, pd.DOB, c.VISIT_DATE_FROM, c.CPT_CODE, c.MODIFIER_CODE1, c.MODIFIER_CODE2,  c.PLAN_BALANCE, sb.TOTAL_SUBMISSIONS, sb.LAST_SUBMISSION_DATE FROM U_FOLLOWUP_BUCKET fb JOIN U_CHARGES c ON fb.VISIT_SEQ_NUM = c.VISIT_SEQ_NUM LEFT JOIN U_PATIENT_DEMOGRAPHIC pd ON c.PATIENT_SEQ_NUM = pd.PATIENT_SEQ_NUM LEFT JOIN U_VISIT_SUBMIT_DATES sb ON fb.VISIT_SEQ_NUM = sb.VISIT_SEQ_NUM WHERE c.PLAN_BALANCE > 0" ;" AND sb.TOTAL_SUBMISSIONS > 3"
 ;"QUIT "SELECT fb.VISIT_SEQ_NUM, fb.SUSPENDED, pd.ACCOUNT_NUM, pd.LAST_NAME, pd.FIRST_NAME, pd.DOB, c.VISIT_DATE_FROM, c.CPT_CODE, c.MODIFIER_CODE1, c.MODIFIER_CODE2,  c.PLAN_BALANCE FROM U_FOLLOWUP_BUCKET fb JOIN U_CHARGES c ON fb.VISIT_SEQ_NUM = c.VISIT_SEQ_NUM LEFT JOIN U_PATIENT_DEMOGRAPHIC pd ON c.PATIENT_SEQ_NUM = pd.PATIENT_SEQ_NUM WHERE c.PLAN_BALANCE > 0"
 ;"QUIT "SELECT * FROM U_VISIT_SUBMIT_DATES"
 QUIT ""
 ;"
FUTOTALS()  ;"SQL Statement for getting bucket totals
 ;"QUIT "SELECT COUNT(*) AS bucket_count, SUM(visit_total) AS total_plan_balance, SUSPENDED FROM ( SELECT fb.VISIT_SEQ_NUM, SUM(c.PLAN_BALANCE) AS visit_total FROM U_FOLLOWUP_BUCKET fb JOIN U_CHARGES c ON fb.VISIT_SEQ_NUM = c.VISIT_SEQ_NUM WHERE c.PLAN_BALANCE > 0 GROUP BY fb.VISIT_SEQ_NUM )"
 QUIT "SELECT COUNT(*) AS bucket_count, SUM(visit_total) AS total_plan_balance, SUM(CASE WHEN fb_sub.SUSPENDED = 'Y' THEN 1 ELSE 0 END) AS suspended_y_count FROM (SELECT fb.VISIT_SEQ_NUM, fb.SUSPENDED, SUM(c.PLAN_BALANCE) AS visit_total FROM U_FOLLOWUP_BUCKET fb JOIN U_CHARGES c ON fb.VISIT_SEQ_NUM = c.VISIT_SEQ_NUM WHERE c.PLAN_BALANCE > 0 GROUP BY fb.VISIT_SEQ_NUM, fb.SUSPENDED) fb_sub"
 ;"
FUTSCH()   ;"SQL Statement for getting future appointments (next 7 days)
 QUIT "SELECT a.PROVIDER,a.ACCOUNT_NUM,a.PATIENT_LAST_NAME,a.PATIENT_FIRST_NAME,d.DOB,a.APPOINTMENT_DATE,a.REASON,s.DURATION,a.COMMENTS,a.APPOITMENT_STATUS,a.CHECKIN_BY,s.COMMENTS,s.ENTRY_DATE FROM U_PATIENT_APPOINTMENTS a JOIN U_SLOTS s ON a.SLOT_SEQ_NUM = s.SLOT_SEQ_NUM JOIN U_PATIENT_DEMOGRAPHIC d ON a.PATIENT_SEQ_NUM = d.PATIENT_SEQ_NUM WHERE TRUNC(a.APPOINTMENT_DATE) BETWEEN TRUNC(SYSDATE) + 1 AND TRUNC(SYSDATE) + 6 AND a.APPOITMENT_STATUS = 'SCHEDULED'" 
 ;"
PAFSDUE()   ;"SQL Statement for getting all missing BC/BS PAFs for this year
 QUIT "SELECT LAST_NAME, FIRST_NAME, CPT_CODE, VISIT_DATE_FROM, DOB, ACCOUNT_NUM, PATIENT_SEQ_NUM FROM (SELECT pd.LAST_NAME, pd.FIRST_NAME, pd.DOB, pd.ACCOUNT_NUM, pd.PATIENT_SEQ_NUM, uc.CPT_CODE, uc.VISIT_DATE_FROM, ROW_NUMBER() OVER (PARTITION BY pd.PATIENT_SEQ_NUM ORDER BY uc.VISIT_DATE_FROM DESC) AS rn FROM U_CHARGES uc JOIN U_PATIENT_DEMOGRAPHIC pd ON uc.PATIENT_SEQ_NUM = pd.PATIENT_SEQ_NUM JOIN U_PATIENT_PLANS pp ON pd.PATIENT_SEQ_NUM = pp.PATIENT_SEQ_NUM JOIN U_PLAN p ON pp.PLAN_SEQ_NUM = p.PLAN_SEQ_NUM WHERE uc.VISIT_DATE_FROM > TO_DATE('2025-01-01', 'YYYY-MM-DD') AND uc.CPT_CODE LIKE '99%' AND p.PLAN LIKE '%BCBS_A%' AND pp.ACTIVE_PLAN = 'Y' AND NOT EXISTS (SELECT 1 FROM U_CHARGES uc2 WHERE uc2.PATIENT_SEQ_NUM = uc.PATIENT_SEQ_NUM AND uc2.VISIT_DATE_FROM > TO_DATE('2025-01-01', 'YYYY-MM-DD') AND uc2.CPT_CODE = '96161')) WHERE rn = 1 ORDER BY LAST_NAME, FIRST_NAME"
 ;"
SUBGT3XS()    ;"SQL Statement for claims submitted more than 3 times 
 QUIT "SELECT fb.VISIT_SEQ_NUM, pd.ACCOUNT_NUM, pd.LAST_NAME, pd.FIRST_NAME, pd.DOB, c.VISIT_DATE_FROM, c.CPT_CODE, c.MODIFIER_CODE1, c.MODIFIER_CODE2, c.PLAN_BALANCE, sb.TOTAL_SUBMISSIONS, sb.LAST_SUBMISSION_DATE, fb.SUSPENDED FROM U_FOLLOWUP_BUCKET fb JOIN U_CHARGES c ON fb.VISIT_SEQ_NUM = c.VISIT_SEQ_NUM JOIN U_PATIENT_DEMOGRAPHIC pd ON c.PATIENT_SEQ_NUM = pd.PATIENT_SEQ_NUM JOIN VIEWS.U_VISIT_SUBMIT_DATES sb ON fb.VISIT_SEQ_NUM = sb.VISIT_SEQ_NUM WHERE c.PLAN_BALANCE > 0 AND sb.TOTAL_SUBMISSIONS > 3"
 ;"
PHQ9PD()      ;"SQL Statement to get last paid PHQ-9 billing details
 ;"QUIT "SELECT CPT_CODE, MODIFIER_CODE1, MODIFIER_CODE2, VISIT_DATE_FROM, ICD_9_CODE1, ICD_9_CODE2, ACCOUNT_NUM, FIRST_NAME, LAST_NAME, SSN, SEX, DOB, PLAN_PAYMENT, ICD_9_CODE3 FROM (SELECT c.CPT_CODE, c.MODIFIER_CODE1, c.MODIFIER_CODE2, c.VISIT_DATE_FROM, c.ICD_9_CODE1, c.ICD_9_CODE2, p.ACCOUNT_NUM, p.FIRST_NAME, p.LAST_NAME, p.SSN, p.SEX, p.DOB, c.PLAN_PAYMENT, c.ICD_9_CODE3, ROW_NUMBER() OVER (PARTITION BY c.PATIENT_SEQ_NUM ORDER BY c.VISIT_DATE_FROM DESC) AS rn FROM U_CHARGES c JOIN U_PATIENT_DEMOGRAPHIC p ON c.PATIENT_SEQ_NUM = p.PATIENT_SEQ_NUM WHERE c.CPT_CODE IN ('G0444','96127') AND c.PLAN_PAYMENT > 0 AND c.VISIT_DATE_FROM >= ADD_MONTHS(SYSDATE, -36)) sub WHERE rn = 1" 
  QUIT "SELECT CPT_CODE, MODIFIER_CODE1, MODIFIER_CODE2, VISIT_DATE_FROM, ICD_9_CODE1, ICD_9_CODE2, ACCOUNT_NUM, FIRST_NAME, LAST_NAME, SSN, SEX, DOB, PLAN_PAYMENT, ICD_9_CODE3 FROM (SELECT c.CPT_CODE, c.MODIFIER_CODE1, c.MODIFIER_CODE2, c.VISIT_DATE_FROM, c.ICD_9_CODE1, c.ICD_9_CODE2, p.ACCOUNT_NUM, p.FIRST_NAME, p.LAST_NAME, p.SSN, p.SEX, p.DOB, c.PLAN_PAYMENT, c.ICD_9_CODE3, ROW_NUMBER() OVER (PARTITION BY c.PATIENT_SEQ_NUM ORDER BY c.VISIT_DATE_FROM DESC) AS rn FROM U_CHARGES c JOIN U_PATIENT_DEMOGRAPHIC p ON c.PATIENT_SEQ_NUM = p.PATIENT_SEQ_NUM WHERE c.CPT_CODE IN ('G0444','96127') AND c.PLAN_PAYMENT > 0) sub WHERE rn = 1"
 ;" 
PGBILSQL(DATEFROM,DATETO)  ;"GET LAB CHARGES FOR TIMEPERIOD FOR PATHGROUP BILLS
  QUIT "SELECT pt.LAST_NAME, pt.FIRST_NAME, uc.VISIT_DATE_FROM, uc.PLAN_PAYMENT, uc.CPT_CODE, UC.PLAN_BALANCE FROM U_CHARGES uc JOIN U_PATIENT_DEMOGRAPHIC pt ON uc.PATIENT_SEQ_NUM = pt.PATIENT_SEQ_NUM WHERE uc.CPT_CODE BETWEEN '80000' AND '89999' AND uc.VISIT_DATE_FROM >= TO_DATE('"_DATEFROM_"','MM/DD/YYYY') AND uc.VISIT_DATE_FROM <= TO_DATE('"_DATETO_"','MM/DD/YYYY')"
  ;"
 ;"==================================================================================================
 ;"===================================   MAIN SQL ROUTINES    =======================================
 ;"================================================================================================== 
FILEWAIT(FILEPATHNAME,TIMEOUTMINS,DEBUGMODE,FORCEREMOVE)  ;"
  ;"Purpose: This function is used to ensure a file doesn't exists, or if it does
  ;"         wait for it to be removed.
  ;"         It will return a 1 if the file isn't there or is removed in the 
  ;"         appropriate amount of time (TIMEOUTMINS)
  ;"Input: FILEPATHNAME - File name with path included
  ;"       TIMEOUTMINS - Number of minutes to wait before timing out
  ;"       DEBUGMODE - if 1 will write status
  ;"       FORCEREMOVE - delete the file if it still exists and is older than 1 hour
  NEW TMGRESULT SET TMGRESULT="1^OK"
  SET FORCEREMOVE=+$G(FORCEREMOVE)
  IF DEBUGMODE=1 WRITE "CHECKING FOR FILE: ",FILEPATHNAME,!
  NEW RUNCOUNT SET RUNCOUNT=12*TIMEOUTMINS  ;"SET COUNT BASED ON RUNNING EVERY 5 SECONDS (12 TIMES PER MINUTE)
  ;"
  IF $$FILEXIST^TMGIOUTL(FILEPATHNAME) DO  ;"HERE WE WILL WAIT 
  . IF DEBUGMODE=1 WRITE "FILE EXISTS. WAITING FOR IT TO BE REMOVED"
  . NEW I,DONE SET DONE=0
  . FOR I=1:1:RUNCOUNT DO  QUIT:DONE=1   
  . . HANG 5   ;"Check every 5 seconds 
  . . IF DEBUGMODE=1 WRITE "."
  . . IF '$$FILEXIST^TMGIOUTL(FILEPATHNAME) SET DONE=1  ;"File is gone an process can continue
  . IF DEBUGMODE=1 WRITE !
  ELSE  DO  GOTO FDDN
  . IF DEBUGMODE=1 WRITE "FILE DOES NOT EXIST.",!
  ;"
  ;"Check to see if the file still exists after the waiting
  IF $$FILEXIST^TMGIOUTL(FILEPATHNAME) DO
  . IF FORCEREMOVE=1 DO
  . . NEW FILEMODDT SET FILEMODDT=$$FILEMOD^TMGKERNL(FILEPATHNAME)
  . . NEW TIMEDIFF SET TIMEDIFF=$$TIMEDIFF^TMGDATE(FILEMODDT,$$TODAY^TMGDATE_"."_$$NOW^TMGDATE)
  . . IF TIMEDIFF>60 DO
  . . . DO DELFILE^TMGIOUTL(FILEPATHNAME)    ;"REMOVE THE FILE IF IT HASN'T BEEN ALTERED IN 60 MINUTES OR MORE
  . . ELSE  DO
  . . . IF DEBUGMODE=1 WRITE "FILE ",FILEPATHNAME," STILL EXISTS AFTER WAITING",!
  . . . SET TMGRESULT="-1^FILE STILL EXISTS"
  ELSE  DO
  . IF DEBUGMODE=1 WRITE "FILE ",FILEPATHNAME," WAS SUCCESSFULLY REMOVED",!
FDDN
  QUIT TMGRESULT
  ;"
SQLREQUEST(SQLSTATEMENT,SQLOUTPATH,SQLOUTNAME,DEBUGMODE,LOGFILE)  ;"
 ;"Purpose: This will be the main function to submit the SQL statement and watch for the 
 ;"         process to finish.
 ;"Input (All strings): SQLSTATEMENT - The SQL query to be ran
 ;"       SQLOUTPATH - The file path to have the data stored in
 ;"       SQLOUTNAME - The file name to have the data stored in
 ;"       DEBUGMODE - 0 or 1
 ;"       LOGFILE - The file path/name to log details for SQL Request
 ;"Output: Results will be 1^SUCCESS or 1^Error Message
 DO LOGLINE(LOGFILE,"BEGINNING SQL QUERY",0,0)
 SET DEBUGMODE=+$G(DEBUGMODE)
 NEW SQLOUTFILE SET SQLOUTFILE=SQLOUTPATH_SQLOUTNAME
 NEW RUNNINGCNT SET RUNNINGCNT=0  ;"Keep track of how many times query was attempted
 NEW TMGRESULT SET TMGRESULT="1^SUCCESS"
 NEW FLAGFILE SET FLAGFILE=$$SQLDIRECTORY+$$SQLRUNNINGFILE
 NEW REQUESTFILE SET REQUESTFILE=$$SQLDIRECTORY_$$SQLREQUESTFILE 
 NEW OUTFILE SET OUTFILE=$$SQLDIRECTORY_SQLOUTNAME
 ;"
 ;"Check for an existing request file
 NEW TMGFILERESULT
 SET TMGFILERESULT=$$FILEWAIT(REQUESTFILE,5,DEBUGMODE)  ;"LOOK FOR ANOTHER PROCESSES REQUEST FILE FIRST
 IF +TMGFILERESULT'=1 DO  GOTO SQLDN 
 . SET TMGRESULT="-1^Another request file existed and was never handled: ("_REQUESTFILE_")"  
 ;"
 ;"Check for active flag file
 SET TMGFILERESULT=$$FILEWAIT(FLAGFILE,5,DEBUGMODE)  ;"LOOK FOR ANOTHER FLAG FILE
 IF +TMGFILERESULT'=1 DO  GOTO SQLDN 
 . SET TMGRESULT="-1^Running file was never removed: ("_FLAGFILE_")"  
 ;"
 ;"No other process is running so we can continue
 ;"Does output file already exist? If so delete.
 IF DEBUGMODE=1 WRITE "CHECKING FOR OUTPUT FILE.",!
 IF $$FILEXIST^TMGIOUTL(SQLOUTFILE) DO
 . IF DEBUGMODE=1 WRITE "OUTPUT FILE FOUND. DELETING.",!
 . DO DELFILE^TMGIOUTL(SQLOUTFILE)
 ;"
 ;"Write out SQL Request file
 IF DEBUGMODE=1 WRITE "WRITING OUT SQL FILE.",!
 NEW TEXTTOSEND SET TEXTTOSEND=SQLSTATEMENT_"^"_SQLOUTFILE
 NEW HANDLE SET HANDLE="TMGSQL"
 NEW PATH SET PATH=$$SQLDIRECTORY
 NEW FILENAME SET FILENAME=$$SQLREQUESTFILE
 DO OPEN^%ZISH(HANDLE,PATH,FILENAME,"W")
 IF POP DO  GOTO SQLDN
 . SET TMGRESULT="-1^Unable to open file for writing: "_PATH_FILENAME
 USE IO
 WRITE TEXTTOSEND
 DO CLOSE^%ZISH(HANDLE)      
 ;"
 ;"Wait for processing to begin (watch for SQLRequest to be removed, which means it is in processing)

 SET TMGFILERESULT=$$FILEWAIT(REQUESTFILE,5,DEBUGMODE)  ;"LOOK FOR OUR REQUEST FILE TO BE ACCEPTED (It will be deleted)
 IF +TMGFILERESULT'=1 DO  GOTO SQLDN 
 . SET TMGRESULT="-1^Our request file was never handled: ("_REQUESTFILE_")"
 ;"
 ;"Wait for output file to exist (watch for SQLOUTFILE to be created)
 ;"
 IF DEBUGMODE=1 WRITE "WAITING FOR OUTPUT FILE TO BE CREATED: ",OUTFILE,!
 IF '$$FILEXIST^TMGIOUTL(OUTFILE) DO  ;"HERE WE WILL WAIT FOR 5 MINUTES FOR CURRENT PROCESS TO FINISH. IF WE HIT 5 MINUTES, SEND ERROR MESSAGE
 . NEW I,DONE SET DONE=0
 . FOR I=1:1:60 DO  QUIT:DONE=1   ;"Run 24 times (which will translate to 5 minutes since it hangs for 15 seconds each check)
 . . IF DEBUGMODE=1 WRITE "."
 . . HANG 5   ;"Check every 5 second 
 . . IF $$FILEXIST^TMGIOUTL(OUTFILE) SET DONE=1  ;"File is gone an process can continue
 . IF DEBUGMODE=1 WRITE !
 HANG 5 ;" ALLOW 5 SECONDS TO ENSURE WRITE IS DONE 
 IF '$$FILEXIST^TMGIOUTL(OUTFILE) DO  GOTO SQLDN
 . IF DEBUGMODE=1 WRITE "TIMED OUT WAITING... QUITTING.",!
 . SET TMGRESULT="-1^TIME OUT. OUTPUT FILE WAS NEVER CREATED. WAITED 5 MINUTES."
SQLDN 
 QUIT TMGRESULT
 ;" 
OPENCHCK()  ;" 
 ;" This function will determine if a process is running during a time our office is considered open
 NEW TMGRESULT SET TMGRESULT=1  ;"DEFAULT TO YES
 ;"
 ;"CHECK THE DAY
 NEW X DO NOW^%DTC
 DO DW^%DTC
 NEW DAYS SET DAYS="MONDAY,TUESDAY,THURSDAY,FRIDAY"
 ;"NEW DAYS SET DAYS="MONDAY,TUESDAY,WEDNESDAY,THURSDAY,FRIDAY"
 IF DAYS'[X SET TMGRESULT=0 GOTO OCDN ;"NOT THE PROPER DAY
 ;"
 ;"IF THE APPROPRIATE DAY, CHECK THE HOUR TO ENSURE WE ARE IN THE PROPER TIMEFRAME
 NEW TIME SET TIME=$E($$NOW^TMGDATE,1,2)  ;"GET THE HOUR
 IF (TIME<8)!(TIME>18) SET TMGRESULT=0  ;"DON'T RUN BEFORE 8AM OR AFTER 6PM
OCDN
 QUIT TMGRESULT
 ;"
OPENDAY()  ;" 
 ;" This function will determine if a process is running during a day our office is considered open
 NEW TMGRESULT SET TMGRESULT=1  ;"DEFAULT TO YES
 ;"
 ;"CHECK THE DAY
 NEW X DO NOW^%DTC
 DO DW^%DTC
 NEW DAYS SET DAYS="MONDAY,TUESDAY,THURSDAY,FRIDAY"
 IF DAYS'[X SET TMGRESULT=0 ;"NOT THE PROPER DAY
 QUIT TMGRESULT 
 ;"
 ;"==================================================================================================
 ;"=============================   OPTION ENTRY POINTS ARE BELOW    =================================
 ;"==================================================================================================
GTSCHSTA();"   GET CURRENT SCHEDULE STATUS
 ;"Purpose: Get the current schedule status
 ;"
 IF $$OPENCHCK=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$SCHSTATF_$$DATAFILETYPE
 SET LOGFILE=$$SCHSTATF_$$LOGFILETYPE
 ;"
 NEW TMGRESULT
 SET TMGRESULT=$$SQLREQUEST($$SQLSTAT(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . DO MAKEALERT("ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2)) 
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW FILING",0,0)
 DO LOADONE^TMGSSQL4(LOGFILE)
 DO LOGLINE(LOGFILE,"SCHEDULE UPDATE COMPLETE.",0,0)
 QUIT
 ;"
GTFTRSCH();"   GET FUTURE APPOINTMENTS
 ;"Purpose: Get the current schedule status
 ;"
 IF $$OPENCHCK=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$FUTSCHDF_$$DATAFILETYPE
 SET LOGFILE=$$FUTSCHDF_$$LOGFILETYPE
 ;"
 NEW TMGRESULT
 SET TMGRESULT=$$SQLREQUEST($$SQLAPPT(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . DO MAKEALERT("ERROR WITH FUTURE SCHEDULE SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH FUTURE SCHEDULE SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW FILING",0,0)
 DO HNDLTASK^TMGSSQL5(LOGFILE)	 
 DO LOGLINE(LOGFILE,"SCHEDULE UPDATE COMPLETE.",0,0)
 QUIT
 ;" 
GTPTDEMO()  ;"   GET PATIENT DEMOGRAPHICS 
 ;"Purpose: Get the patient demographics
 ;"
 IF $$OPENDAY=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN 
 NEW TMGRESULT
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$PTDEMOF_$$DATAFILETYPE
 SET LOGFILE=$$PTDEMOF_$$LOGFILETYPE
 ;"
 SET TMGRESULT=$$SQLREQUEST($$SQLDEMO(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . DO MAKEALERT("ERROR WITH PATIENT DEMO SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH PATIENT DEMO SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW FILING",0,0)
 DO RUNNOW^TMGSSQL2(LOGFILE)	 
 QUIT
 ;" 
GTPTENC()  ;"   GET PATIENT ENCOUNTER INFO (CPTS AND ICDS) 
 ;"Purpose: Get the patient demographics
 ;"
 IF $$OPENDAY=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN 
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$ENCOUNTF_$$DATAFILETYPE
 SET LOGFILE=$$ENCOUNTF_$$LOGFILETYPE
 ;"
 NEW TMGRESULT
 SET TMGRESULT=$$SQLREQUEST($$SQLENC(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . DO MAKEALERT("ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW FILING",0,0)
 DO IMPCPTS^TMGSSQL3(LOGFILE)	 
 QUIT
 ;" 
GTSCH4BC(TMGRESULT,DATE)  ;"
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$RANDOMF_$$DATAFILETYPE
 SET LOGFILE=$$RANDOMF_$$LOGFILETYPE
 ;"
 SET TMGRESULT=$$SQLREQUEST($$SQLFORMS(DATE),$$WINDIR(),DATAFILE,0,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . ;"DO MAKEALERT("ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW RETURNING",0,0)
 SET TMGRESULT=$$MOVE^TMGKERNL($$SQLDIRECTORY()_DATAFILE,"/mnt/WinPublic/vista/Scanner/Documents/appointments1.csv") 
 QUIT
 ;" 
VALIDDT(DATE,OUTSQL,OUTFM) ;"
 ;"CHECK DATE TO ENSURE IT IS VALID. IF SO RETURN IN BOTH SQL FORMAT AND FM FORMAT
 SET OUTSQL=0,OUTFM=0
 NEW TMGRESULT
 NEW %DT,X,Y
 SET %DT="E",X=DATE
 DO ^%DT
 SET OUTFM=Y
 SET TMGRESULT=OUTFM>0
 IF TMGRESULT>0 DO
 . NEW MM,DD,YYYY
 . SET MM=$E(OUTFM,4,5)
 . SET DD=$E(OUTFM,6,7)
 . SET YYYY=1700+$E(OUTFM,1,3)
 . SET OUTSQL=MM_"-"_DD_"-"_YYYY
 QUIT TMGRESULT
 ;"
BILITEMS()  ;"THIS WILL GET ALL THE BILLABLE ITEMS AND COMPARE TO WHAT HAS ALREADY BEEN POSTED
 NEW SQLDATE,FMDATE,INDATE,VALID
 SET VALID=0
 FOR  DO  QUIT:VALID=1
 . WRITE !,"Enter a date (MM/DD/YYYY): "
 . READ INDATE
 . IF INDATE="^" SET VALID=1 QUIT
 . SET VALID=$$VALIDDT(INDATE,.SQLDATE,.FMDATE)
 . IF VALID=1 QUIT
 . ELSE  WRITE "Invalid date format. Please try again.",!
 IF INDATE="^" QUIT
 ;"
 ;"W "INDATE=",INDATE,!,"SQLDATE=",SQLDATE,!,"FMDATE=",FMDATE,!
 ;"QUIT
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$RANDOMF_$$DATAFILETYPE
 SET LOGFILE=$$RANDOMF_$$LOGFILETYPE
 ;"
 WRITE !,"REQUESTING INFORMATION FROM SEQUEL",!
 SET TMGRESULT=$$SQLREQUEST($$BITEMSQL(SQLDATE),$$WINDIR(),DATAFILE,0,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . ;"DO MAKEALERT("ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW RETURNING",0,0)
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 ;"
 NEW BILLEDARR
 NEW IDX SET IDX=0
 FOR  SET IDX=$O(TMGARRAY(IDX)) QUIT:IDX'>0  DO
 . NEW CPT,ACCT
 . SET CPT=$G(TMGARRAY(IDX,3)),ICD=$G(TMGARRAY(IDX,4))
 . SET ACCT=$G(TMGARRAY(IDX,1))
 . SET BILLEDARR(ACCT,"CPT",CPT)=ICD
 ;"TMGARRAY NOW CONTAINS ALL THE ENTERED CHARGES FROM SEQUELMED
 ;"GET BILLABLE ITEMS
 NEW BILLITEMS DO BILLRPC^TMGRPT2(.BILLITEMS,FMDATE,FMDATE,4)
 ;"NEW BILLITEMS DO BILLRPC^TMGRPT2(.BILLITEMS,3250325,3250325,4)
 ;"
 ;"ZWR BILLITEMS
 NEW ACCT SET ACCT=0
 NEW TESTINS SET TESTINS="^AARP / Secure Horizon^BC/BS ADVANTAGE^HUMANA GOLD^MEDICARE^UHC-LIFE1^"
 FOR  SET ACCT=$O(BILLITEMS(ACCT)) QUIT:ACCT'>0  DO
 . NEW TMGDFN SET TMGDFN=+$O(^DPT("TMGS",ACCT,0))
 . NEW INS SET INS="^"_$$GETPINS^TMGTIUT3(TMGDFN)_"^" 
 . NEW PTNAME SET PTNAME=$P($G(^DPT(TMGDFN,0)),"^",1)
 . IF '$D(BILLEDARR(ACCT)) SET PTNAME=PTNAME_" MISSING TICKET??"
 . NEW NAMESHOWN SET NAMESHOWN=0
 . ;"WRITE "== ACCT ",ACCT," ",PTNAME," ==",!
 . NEW CPT SET CPT=0
 . FOR  SET CPT=$O(BILLITEMS(ACCT,"CPT",CPT)) QUIT:CPT=""  DO
 . . NEW TEMPCPT SET TEMPCPT=$P(CPT," ",1)
 . . IF (TEMPCPT="G2211")&(TESTINS'[INS) QUIT   ;" NO G2211 IF NOT MEDICARE PRODUCT
 . . ;"WRITE " CHECKING FOR CPT ",CPT
 . . IF ($D(BILLEDARR(ACCT,"CPT",TEMPCPT)))!($D(BILLEDARR(ACCT,"CPT","""CPT"""))) DO 
 . . . ;"WRITE ". It is billed",!
 . . ELSE  DO
 . . . IF NAMESHOWN=0 DO
 . . . . WRITE "== ACCT ",ACCT," ",PTNAME," ==",!
 . . . . SET NAMESHOWN=1
 . . . WRITE "CPT ",CPT," -- NOT BILLED!!!!",!
 QUIT
 ;" 
GTAPPTCALLS();"   GET CURRENT SCHEDULE STATUS
 ;"Purpose: Get the current schedule status
 ;"
 IF $$OPENCHCK=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$APPTCALLF_$$DATAFILETYPE
 SET LOGFILE=$$APPTCALLF_$$LOGFILETYPE
 ;"
 NEW TMGRESULT
 ;"SET TMGRESULT=$$SQLREQUEST($$SQLSTAT(),$$WINDIR(),DATAFILE,1,LOGFILE)
 SET TMGRESULT=$$SQLREQUEST($$FUTSCH(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . DO MAKEALERT("ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2)) 
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2))
 ;"
 ;"  TO PROCESS THE RESULTS HERE.... 
 DO LOAD^TMGSSQL6($$SQLDIRECTORY()_DATAFILE,1,LOGFILE)	
 ;"DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW FILING",0,0)
 ;"DO LOADONE^TMGSSQL4(LOGFILE)
 ;"DO LOGLINE(LOGFILE,"SCHEDULE UPDATE COMPLETE.",0,0)
 QUIT
 ;" 
 ;"==================================================================================================
 ;"===================================   MAIN LOG ROUTINES    =======================================
 ;"==================================================================================================
 ;"
LOGLINE(LOGFILE,LINE,CLEARFILE,NODTSTAMP) ;"
 ;"This routine will log the sent in LINE to the LOGFILE, which should be FILENAME only
 ;"CLEARFILE - 0 OR 1, if 0 LOG will APPEND. if 1 LOG will CLEAR file
 ;"NODTSTAMP - 0 OR 1, will add a date/time stamp to each line unless this is 1
 SET CLEARFILE=+$G(CLEARFILE)
 SET NODTSTAMP=+$G(NODTSTAMP)
 NEW PREFIX SET PREFIX=$S(NODTSTAMP=0:"["_$$HTE^XLFDT($horolog,"5Z")_"] ",NODTSTAMP=1:"")
 NEW MODE SET MODE=$S(CLEARFILE=0:"A",CLEARFILE=1:"W")
 NEW HANDLE SET HANDLE="TMGSQL"
 NEW PATH SET PATH=$$LOGDIRECTORY
 NEW FILENAME SET FILENAME=LOGFILE
 DO OPEN^%ZISH(HANDLE,PATH,FILENAME,MODE)
 IF POP DO  GOTO SQLDN
 . SET TMGRESULT="-1^Unable to open file for writing: "_PATH_FILENAME
 USE IO
 WRITE PREFIX,LINE,!
 DO CLOSE^%ZISH(HANDLE)   
 QUIT
 ;"
MAKEALERT(MESSAGE)  ;"ONLY ADD AN ALERT IF IT DOESN'T EXIST
 NEW ALRTEXISTS,ALRTRESULT SET ALRTEXISTS=0
 NEW TMGDUZ SET TMGDUZ=150 ;"SET TO EDDIE
 ;
 NEW ALRTDT SET ALRTDT=0
 FOR  SET ALRTDT=$O(^XTV(8992,TMGDUZ,"XQA",ALRTDT)) QUIT:(ALRTDT="")!(ALRTEXISTS=1)  DO
 . NEW ONEMSG SET ONEMSG=$G(^XTV(8992,TMGDUZ,"XQA",ALRTDT,0))
 . SET ONEMSG=$P(ONEMSG,"^",3)
 . IF ONEMSG=MESSAGE SET ALRTEXISTS=1
 IF ALRTEXISTS=0 DO INFRMALT^TMGXQAL(.ALRTRESULT,TMGDUZ,MESSAGE)
 QUIT
 ;"
RUNSQL(TMGRESULT,SQL,FILEPATH,FILENAME)   ;"RPC: TMG SEQUEL RUN SQL
  ;" !!!!  NOTE:  FOR FUTURE USE, IF NEEDED. THIS FUNCTION HAS NOT BEEN IMPLEMENTED NOR TESTED. !!!!
 ;"Entry Point Run random SQL query
 ;"TMGRESULT: Results from query. 1^SUCCESS (file should exist) or -1^ERROR MESSAGE
 ;"SQL: SQL query to run
 ;"FILEPATH: Filepath to store the file
 ;"FILENAME: Filename to store the file in
 SET TMGRESULT="1^FILE IS STORED IN: "_FILEPATH_FILENAME
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$RANDOMF_$$DATAFILETYPE
 SET LOGFILE=$$RANDOMF_$$LOGFILETYPE
 ;"
 NEW TMGRESULT
 SET TMGRESULT=$$SQLREQUEST($$SQLENC(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . SET TMGRESULT="-1^ERROR WITH SQL QUERY: "_$P(TMGRESULT,"^",2)
 
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW FILING",0,0)
 QUIT
 ;"
 ;"==================================================================================================
 ;"===============================   FOLLOWUP BUCKET  ROUTINES    ===================================
 ;"================================================================================================== 
FUBUCKET()  ;"GET FOLLOWUP BUCKET ITEMS AND PRINT REPORT
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$RANDOMF_$$DATAFILETYPE
 SET LOGFILE=$$RANDOMF_$$LOGFILETYPE
 ;"
 ;" START A MENU TO ASK REPORT TYPE
 NEW MENU,IDX,USRPICK
 ;
 SET IDX=0   
 SET MENU(IDX)="Select Followup Bucket Report Type"
 SET MENU($I(IDX))="All claims by age"_$CHAR(9)_"ByAge"
 SET MENU($I(IDX))="All claims by total balance"_$CHAR(9)_"ByBalance"
 SET MENU($I(IDX))="High dollar claims (over $25 due)"_$CHAR(9)_"HighBalance"
 SET MENU($I(IDX))="Balance trend"_$CHAR(9)_"BalanceTrend"
 SET MENU($I(IDX))="Submitted > 4 times"_$CHAR(9)_"Submitted"
 WRITE !
 SET USRPICK=$$MENU^TMGUSRI2(.MENU,"^")
 IF (USRPICK="^")!(USRPICK="") QUIT
 IF USRPICK="ByAge" DO
 . NEW DAYS
 . SET DAYS=0
 . FOR  DO  QUIT:(+DAYS>0)!(DAYS="^")
 . . WRITE !,"Enter an age, in days: "
 . . READ DAYS
 . . IF DAYS="^" QUIT
 . . IF +DAYS'>0 WRITE !,"Invalid number of days. Please try again.",!
 . IF DAYS="^" QUIT
 . WRITE !
 . DO BYAGE(DAYS)
 IF USRPICK="HighBalance" DO
 . NEW DOLLARAMT
 . SET DOLLARAMT=0
 . FOR  DO  QUIT:(+DOLLARAMT>0)!(DOLLARAMT="^")
 . . WRITE !,"Enter dollar amount: "
 . . READ DOLLARAMT
 . . IF DOLLARAMT="^" QUIT
 . . IF +DOLLARAMT'>0 WRITE !,"Invalid dollar amount. Please try again.",!
 . IF DOLLARAMT="^" QUIT
 . WRITE !
 . DO HIGHBAL(DOLLARAMT)
 IF USRPICK="ByBalance" DO
 . WRITE "Not done yet",!
 IF USRPICK="BalanceTrend" DO
 . WRITE !
 . DO BALTREND
 IF USRPICK="Submitted" DO
 . WRITE !
 . DO SUBMITTED
 QUIT
  ;" 
BYAGE(DAYS) 
 SET TMGRESULT=$$SQLREQUEST($$FBUCKSQL(),$$WINDIR(),DATAFILE,0,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . ;"DO MAKEALERT("ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW RETURNING",0,0) 	 
 ;"
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 NEW IDX SET IDX=0
 NEW OUTARR,OLDVISIT,PREVNARVISIT
 FOR  SET IDX=$O(TMGARRAY(IDX)) QUIT:IDX'>0  DO
 . NEW SUBIDX SET SUBIDX=0
 . NEW VISITNUM SET VISITNUM=$G(TMGARRAY(IDX,1))
 . NEW DOS SET DOS=$G(TMGARRAY(IDX,7))
 . NEW DAYSDIFF SET DAYSDIFF=$$DAYSDIFF^TMGDATE($$TODAY^TMGDATE(),$$INTDATE^TMGDATE(DOS))
 . SET VISITNUM=VISITNUM_" DATE OF SERVICE: "_DOS
 . NEW ACCOUNT SET ACCOUNT=$G(TMGARRAY(IDX,3))
 . NEW CPT SET CPT=$G(TMGARRAY(IDX,8))
 . NEW MOD1,MOD2 SET MOD1=$G(TMGARRAY(IDX,9)),MOD2=$G(TMGARRAY(IDX,10))
 . IF MOD1'="" SET CPT=CPT_"-"_MOD1
 . IF MOD2'="" SET CPT=CPT_"&"_MOD2
 . NEW PATIENT SET PATIENT=$G(TMGARRAY(IDX,4))_","_$G(TMGARRAY(IDX,5))_" ("_$G(TMGARRAY(IDX,6))_") - "_ACCOUNT
 . NEW BALANCE SET BALANCE=$P($G(TMGARRAY(IDX,11)),$C(13),1)
 . ;"  NOTE! I added DAYSDIFF so it would arrange by the oldest one first  4/21/25
 . IF CPT["90677" SET PREVNARVISIT(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 . ELSE  IF DAYSDIFF>DAYS SET OLDVISIT(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 . ELSE  SET OUTARR(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 ;"
 ;" NOW ADJUST PREVNAR ARRAYS
 NEW DAYSDIFF SET DAYSDIFF=0
 FOR  SET DAYSDIFF=$O(PREVNARVISIT(DAYSDIFF)) QUIT:DAYSDIFF'>0  DO
 . NEW PATIENT SET PATIENT=""
 . FOR  SET PATIENT=$O(PREVNARVISIT(DAYSDIFF,PATIENT)) QUIT:PATIENT=""  DO
 . . NEW VISITNUM SET VISITNUM=""
 . . FOR  SET VISITNUM=$O(PREVNARVISIT(DAYSDIFF,PATIENT,VISITNUM)) QUIT:VISITNUM=""  DO
 . . . IF $D(OLDVISIT(DAYSDIFF,PATIENT,VISITNUM)) DO
 . . . . MERGE PREVNARVISIT(DAYSDIFF,PATIENT,VISITNUM)=OLDVISIT(DAYSDIFF,PATIENT,VISITNUM)
 . . . . KILL OLDVISIT(DAYSDIFF,PATIENT,VISITNUM)
 ;"
 NEW %ZIS,IOP
 SET IOP="S121-LAUGHLIN-LASER"
 DO ^%ZIS  ;"standard device call
 IF POP QUIT
 USE IO
 ;"
 WRITE !
 WRITE "************************************************************",!
 WRITE "           Sequel Followup Bucket Report, By Age (>",DAYS," days old)",!
 WRITE "                     " SET Y=X DO DD^%DT WRITE Y,!
 WRITE "              Please deliver this report to Lindsey",!
 WRITE "************************************************************",!
 WRITE "                                            (From TMGSSQL1.m)",!!
 IF $D(PREVNARVISIT) DO
 . WRITE !,"**** VISITS WITH PREVNAR IMMUNIZATIONS - HIGH PRIORITY!!!! ****",!
 . DO WRITEARR(.PREVNARVISIT)
 WRITE !,"***********************************"
 WRITE !,"**** VISITS OLDER THAN ",DAYS," DAYS ****"
 WRITE !,"***********************************",!
 DO WRITEARR(.OLDVISIT)
 DO ^%ZISC  ;" Close the output device
 QUIT
 ;"
WRITEARR(OUTARR)  ;"OUTPUT FOLLOWUP BUCKET ARRAY
 NEW DAYSDIFF SET DAYSDIFF=999999
 FOR  SET DAYSDIFF=$O(OUTARR(DAYSDIFF),-1) QUIT:DAYSDIFF'>0  DO 
 . NEW PATIENT SET PATIENT=""
 . FOR  SET PATIENT=$O(OUTARR(DAYSDIFF,PATIENT)) QUIT:PATIENT=""  DO
 . . WRITE "  ==== ",PATIENT," ",DAYSDIFF," DAYS OLD ====",!
 . . NEW VISITNUM SET VISITNUM=""
 . . FOR  SET VISITNUM=$O(OUTARR(DAYSDIFF,PATIENT,VISITNUM)) QUIT:VISITNUM=""  DO
 . . . WRITE "    VISIT NUMBER -> ",VISITNUM," !! ",$G(IMPORTANT(VISITNUM)),!
 . . . NEW CPT SET CPT=""
 . . . FOR  SET CPT=$O(OUTARR(DAYSDIFF,PATIENT,VISITNUM,CPT)) QUIT:CPT=""  DO
 . . . . WRITE "       ",CPT," WITH BALANCE OF $",$G(OUTARR(DAYSDIFF,PATIENT,VISITNUM,CPT)),!
 . . . WRITE !
 . . WRITE !
 QUIT  
 ;"=============================================================
 ;"
HIGHBAL(BALANCECUTOFF)
 SET TMGRESULT=$$SQLREQUEST($$FBUCKSQL(),$$WINDIR(),DATAFILE,0,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . ;"DO MAKEALERT("ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW RETURNING",0,0) 	 
 ;"
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 NEW IDX SET IDX=0
 NEW OUTARR,OLDVISIT,PREVNARVISIT
 FOR  SET IDX=$O(TMGARRAY(IDX)) QUIT:IDX'>0  DO
 . NEW SUBIDX SET SUBIDX=0
 . NEW VISITNUM SET VISITNUM=$G(TMGARRAY(IDX,1))
 . NEW DOS SET DOS=$G(TMGARRAY(IDX,7))
 . NEW DAYSDIFF SET DAYSDIFF=$$DAYSDIFF^TMGDATE($$TODAY^TMGDATE(),$$INTDATE^TMGDATE(DOS))
 . SET VISITNUM=VISITNUM_" DATE OF SERVICE: "_DOS
 . NEW ACCOUNT SET ACCOUNT=$G(TMGARRAY(IDX,3))
 . NEW CPT SET CPT=$G(TMGARRAY(IDX,8))
 . NEW MOD1,MOD2 SET MOD1=$G(TMGARRAY(IDX,9)),MOD2=$G(TMGARRAY(IDX,10))
 . IF MOD1'="" SET CPT=CPT_"-"_MOD1
 . IF MOD2'="" SET CPT=CPT_"&"_MOD2
 . NEW PATIENT SET PATIENT=$G(TMGARRAY(IDX,4))_","_$G(TMGARRAY(IDX,5))_" ("_$G(TMGARRAY(IDX,6))_") - "_ACCOUNT
 . NEW BALANCE SET BALANCE=$P($G(TMGARRAY(IDX,11)),$C(13),1)
 . IF CPT["90677" SET PREVNARVISIT(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 . ELSE  IF BALANCE>BALANCECUTOFF SET OLDVISIT(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 . ELSE  SET OUTARR(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 ;"
 ;" NOW ADJUST PREVNAR ARRAYS
 NEW DAYSDIFF SET DAYSDIFF=0
 FOR  SET DAYSDIFF=$O(PREVNARVISIT(DAYSDIFF)) QUIT:DAYSDIFF'>0  DO
 . NEW PATIENT SET PATIENT=""
 . FOR  SET PATIENT=$O(PREVNARVISIT(DAYSDIFF,PATIENT)) QUIT:PATIENT=""  DO
 . . NEW VISITNUM SET VISITNUM=""
 . . FOR  SET VISITNUM=$O(PREVNARVISIT(DAYSDIFF,PATIENT,VISITNUM)) QUIT:VISITNUM=""  DO
 . . . IF $D(OLDVISIT(DAYSDIFF,PATIENT,VISITNUM)) DO
 . . . . MERGE PREVNARVISIT(DAYSDIFF,PATIENT,VISITNUM)=OLDVISIT(DAYSDIFF,PATIENT,VISITNUM)
 . . . . KILL OLDVISIT(DAYSDIFF,PATIENT,VISITNUM)
 ;"
 NEW %ZIS,IOP
 SET IOP="S121-LAUGHLIN-LASER"
 DO ^%ZIS  ;"standard device call
 IF POP QUIT
 USE IO
 ;"
 WRITE !
 WRITE "************************************************************",!
 WRITE "           Sequel Followup Bucket Report, High Balance (>$",BALANCECUTOFF,")",!
 WRITE "                     " SET Y=X DO DD^%DT WRITE Y,!
 WRITE "              Please deliver this report to Lindsey",!
 WRITE "************************************************************",!
 WRITE "                                            (From TMGSSQL1.m)",!!
 IF $D(PREVNARVISIT) DO
 . WRITE !,"**** VISITS WITH PREVNAR IMMUNIZATIONS - HIGH PRIORITY!!!! ****",!
 . DO WRITEARR(.PREVNARVISIT)
 WRITE !,"***********************************"
 WRITE !,"**** VISITS WITH BALANCES OVER $",BALANCECUTOFF," ****"
 WRITE !,"***********************************",!
 DO WRITEARR(.OLDVISIT)
 DO ^%ZISC  ;" Close the output device
 QUIT
 ;"  
SUBMITTED()
 SET TMGRESULT=$$SQLREQUEST($$FUSUBMIT(),$$WINDIR(),DATAFILE,0,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . ;"DO MAKEALERT("ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW RETURNING",0,0) 	 
 ;"
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 QUIT
 ;"
 NEW IDX SET IDX=0
 NEW OUTARR,OLDVISIT,PREVNARVISIT
 FOR  SET IDX=$O(TMGARRAY(IDX)) QUIT:IDX'>0  DO
 . NEW SUBIDX SET SUBIDX=0
 . NEW VISITNUM SET VISITNUM=$G(TMGARRAY(IDX,1))
 . NEW DOS SET DOS=$G(TMGARRAY(IDX,7))
 . NEW DAYSDIFF SET DAYSDIFF=$$DAYSDIFF^TMGDATE($$TODAY^TMGDATE(),$$INTDATE^TMGDATE(DOS))
 . SET VISITNUM=VISITNUM_" DATE OF SERVICE: "_DOS
 . NEW ACCOUNT SET ACCOUNT=$G(TMGARRAY(IDX,3))
 . NEW CPT SET CPT=$G(TMGARRAY(IDX,8))
 . NEW MOD1,MOD2 SET MOD1=$G(TMGARRAY(IDX,9)),MOD2=$G(TMGARRAY(IDX,10))
 . IF MOD1'="" SET CPT=CPT_"-"_MOD1
 . IF MOD2'="" SET CPT=CPT_"&"_MOD2
 . NEW PATIENT SET PATIENT=$G(TMGARRAY(IDX,4))_","_$G(TMGARRAY(IDX,5))_" ("_$G(TMGARRAY(IDX,6))_") - "_ACCOUNT
 . NEW BALANCE SET BALANCE=$P($G(TMGARRAY(IDX,11)),$C(13),1)
 . IF CPT["90677" SET PREVNARVISIT(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 . ELSE  IF BALANCE>BALANCECUTOFF SET OLDVISIT(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 . ELSE  SET OUTARR(DAYSDIFF,PATIENT,VISITNUM,CPT)=BALANCE
 ;"
 NEW %ZIS,IOP
 SET IOP="S121-LAUGHLIN-LASER"
 DO ^%ZIS  ;"standard device call
 IF POP QUIT
 USE IO
 ;"
 WRITE !
 WRITE "************************************************************",!
 WRITE "         Sequel Followup Bucket Report, Claims submitted > 4 times",!
 WRITE "                     " SET Y=X DO DD^%DT WRITE Y,!
 WRITE "              Please deliver this report to Lindsey",!
 WRITE "************************************************************",!
 WRITE "                                            (From TMGSSQL1.m)",!!
 WRITE !,"***********************************"
 WRITE !,"**** VISITS SUBMITTED > 4 TIMES ****"
 WRITE !,"***********************************",!
 DO WRITEARR(.OLDVISIT)
 DO ^%ZISC  ;" Close the output device
 QUIT
 ;"  
UBALTRND()  ;"UPDATE THE BALANCE TREND - SCHEDULED TO RUN DAILY
 IF $$OPENDAY=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN 
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$RANDOMF_$$DATAFILETYPE
 SET LOGFILE=$$RANDOMF_$$LOGFILETYPE
 SET TMGRESULT=$$SQLREQUEST($$FUTOTALS(),$$WINDIR(),DATAFILE,0,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . ;"DO MAKEALERT("ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW RETURNING",0,0) 	 
 ;"
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 ;"
 NEW TOTALCLAIMS,TOTALDOLLARS,SUSPENDED
 SET TOTALCLAIMS=$G(TMGARRAY(1,1))
 SET TOTALDOLLARS=$P($G(TMGARRAY(1,2)),$C(13),1)
 SET SUSPENDED=$G(TMGARRAY(1,3))
 NEW TMGRESULT SET TMGRESULT=0
 NEW TMGFDA,TMGERR,NEWIEN
 S TMGFDA(22763,"+1,",.01)=$$TODAY^TMGDATE()_"."_$$NOW^TMGDATE()  ; .01 = Specialty Field
 S TMGFDA(22763,"+1,",.02)=TOTALCLAIMS  ; 
 S TMGFDA(22763,"+1,",.03)=TOTALDOLLARS  ; 
 S TMGFDA(22763,"+1,",.04)=SUSPENDED  ; 
 D UPDATE^DIE("","TMGFDA","NEWIEN","TMGERR")
 SET TMGRESULT=+$G(NEWIEN(1))
 QUIT
 ;"
BALTREND() 
 IF $$OPENDAY=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN 
 NEW OUTPUT,IDX,OUTIDX
 SET IDX=0
 NEW LTOT,LAMT,LSUS
 SET (LTOT,LAMT,LSUS,OUTIDX)=0
 FOR  SET IDX=$O(^TMG(22763,IDX)) QUIT:IDX'>0  DO
 . NEW ZN SET ZN=$G(^TMG(22763,IDX,0))
 . NEW DATE,TOT,AMT,SUSPENDED
 . NEW DAY SET DAY=$P($P(ZN,"^",1),".",1)
 . SET DATE=$$EXTDATE^TMGDATE($P(ZN,"^",1))
 . SET TOT=$P(ZN,"^",2)
 . SET AMT=$P(ZN,"^",3)
 . SET SUSPENDED=$P($P(ZN,"^",4),$C(13),1)
 . IF LTOT=0 DO
 . . SET AMT="$"_AMT
 . . SET OUTPUT($I(OUTIDX),DAY)=$J(DATE,-25)_$J(TOT,12)_$J(AMT,14)_$J(SUSPENDED,15)
 . ELSE  DO
 . . NEW CALTOT SET CALTOT=TOT-LTOT,CALTOT="("_CALTOT_")"
 . . NEW CALAMT SET CALAMT=AMT-LAMT,CALAMT="($"_CALAMT_")"
 . . NEW CALSUS SET CALSUS=SUSPENDED-LSUS,CALSUS="("_CALSUS_")"
 . . ;"SET OUTPUT($I(OUTIDX))=$J(DATE,25)_$J(CALTOT,12)_$J(CALAMT,14)_$J(CALSUS,10)
 . . ;NEW CALTOT SET CALTOT="("_TOT-LTOT_")"
 . . ;NEW CALAMT SET CALAMT="($"_AMT-LAMT_")"
 . . ;NEW CALSUS SET CALSUS="("_SUSPENDED-LSUS_")"
 . . SET AMT="$"_AMT
 . . SET OUTPUT($I(OUTIDX),DAY)=$J(DATE,-25)_$J(TOT,12)_CALTOT_$J(AMT,14-$L(CALTOT))_CALAMT_$J(SUSPENDED,15-$L(CALAMT))_CALSUS
 . SET LTOT=TOT
 . SET LAMT=$P(AMT,"$",2)
 . SET LSUS=SUSPENDED
 ;"ZWR OUTPUT
 ;"
 NEW %ZIS,IOP
 SET IOP="S121-LAUGHLIN-LASER"
 DO ^%ZIS  ;"standard device call
 IF POP QUIT
 USE IO
 ;"
 WRITE !
 WRITE "************************************************************",!
 WRITE "         Sequel Followup Bucket Balance Trend Report",!
 WRITE "                     ",$$TODAY^TMGDATE(1)
 WRITE "              Please deliver this report to Eddie",!
 WRITE "************************************************************",!
 WRITE "                                            (From TMGSSQL1.m)",!!
 SET OUTIDX=9999999
 WRITE "DATE",?30,"CLAIMS",?40,"TOTAL DUE",?60,"SUSPENDED",!
 NEW LDAY SET LDAY=0
 FOR  SET OUTIDX=$O(OUTPUT(OUTIDX),-1) QUIT:OUTIDX'>0  DO
 . NEW DAY SET DAY=0
 . FOR  SET DAY=$O(OUTPUT(OUTIDX,DAY)) QUIT:DAY'>0  DO
 . . IF DAY'=LDAY WRITE !
 . . WRITE $G(OUTPUT(OUTIDX,DAY)),!
 . . SET LDAY=DAY
 DO ^%ZISC  ;" Close the output device
 QUIT
 ;"   
PAFNEED();"   GET ALL NEEDED PAFS FOR BCBS ADV 
 ;"Purpose: Get the current schedule status
 ;"
 IF $$OPENCHCK=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$PAFF_$$DATAFILETYPE
 SET LOGFILE=$$PAFF_$$LOGFILETYPE
 ;"
 NEW TMGRESULT
 SET TMGRESULT=$$SQLREQUEST($$PAFSDUE(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . DO MAKEALERT("ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2)) 
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2))
 ;"
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 NEW LWEEK,OLD,OUTIDX
 NEW BDT SET BDT=$$ADDDAYS^TMGDATE("-8")
 SET OUTIDX=0
 FOR  SET OUTIDX=$O(TMGARRAY(OUTIDX)) QUIT:OUTIDX'>0  DO
 . IF $$IGNORPAF(+$G(TMGARRAY(OUTIDX,6)))=1 QUIT   ;" IGNORE THIS PATIENT
 . NEW LDATE SET LDATE=$$INTDATE^TMGDATE($G(TMGARRAY(OUTIDX,4)))
 . IF LDATE>BDT DO
 . . SET LWEEK(OUTIDX)=$G(TMGARRAY(OUTIDX,1))_"^"_$G(TMGARRAY(OUTIDX,2))_"^"_$G(TMGARRAY(OUTIDX,5))_"^"_$G(TMGARRAY(OUTIDX,6))_"^"_$G(TMGARRAY(OUTIDX,4))_"-"_$P($G(TMGARRAY(OUTIDX,3)),$C(13),1) 
 . ELSE  DO
 . . SET OLD(LDATE,OUTIDX)=$G(TMGARRAY(OUTIDX,1))_"^"_$G(TMGARRAY(OUTIDX,2))_"^"_$G(TMGARRAY(OUTIDX,5))_"^"_$G(TMGARRAY(OUTIDX,6))_"^"_$G(TMGARRAY(OUTIDX,4))_"-"_$P($G(TMGARRAY(OUTIDX,3)),$C(13),1)
 ;"
 DO PRINTPAF(.LWEEK,.OLD,"LINDSEY")
 ;"DO PRINTPAF(.LWEEK,.OLD,"DR. DEE")
 QUIT
 ;" 
PRINTPAF(LWEEK,OLD,RECIP)  ;"
 NEW %ZIS,IOP
 SET IOP="S121-LAUGHLIN-LASER"
 DO ^%ZIS  ;"standard device call
 IF POP QUIT
 USE IO
 ;"
 WRITE !
 WRITE "************************************************************",!
 WRITE "             PAFs Needed For BC/BS Adv Patients",!
 WRITE "                     ",$$TODAY^TMGDATE(1),!
 WRITE "              Please deliver this report to ",RECIP,!
 WRITE "************************************************************",!
 WRITE "                                            (From TMGSSQL1.m)",!!
 NEW OUTIDX SET OUTIDX=0
 WRITE "================  LAST WEEK'S PATIENTS =====================",!,!
 WRITE "Patient",?25,"DOB",?40,"Acct Num",?50,"Last Visit Data",!
 FOR  SET OUTIDX=$O(LWEEK(OUTIDX)) QUIT:OUTIDX'>0  DO
 . NEW LINE SET LINE=$G(LWEEK(OUTIDX))
 . WRITE $P(LINE,"^",1),",",$P(LINE,"^",2),?25,$P(LINE,"^",3),?40,$P(LINE,"^",4),?50,$P(LINE,"^",5),!,!
 WRITE !
 ;"
 WRITE "================      OLD PATIENTS     =====================",!,!
 WRITE "Patient",?25,"DOB",?40,"Acct Num",?50,"Last Visit Data",!
 NEW LDATE SET LDATE=0
 FOR  SET LDATE=$O(OLD(LDATE)) QUIT:LDATE'>0  DO
 . SET OUTIDX=0
 . FOR  SET OUTIDX=$O(OLD(LDATE,OUTIDX)) QUIT:OUTIDX'>0  DO
 . . NEW LINE SET LINE=$G(OLD(LDATE,OUTIDX))
 . . WRITE $P(LINE,"^",1),",",$P(LINE,"^",2),?25,$P(LINE,"^",3),?40,$P(LINE,"^",4),?50,$P(LINE,"^",5),!,!
 DO ^%ZISC  ;" Close the output device
 QUIT
 ;"
IGNORPAF(TMGDFN)  ;
 NEW RESULT SET RESULT=0
 NEW ARRAY
 SET ARRAY(323810)=""  ;"DFN TO IGNORE KnoxRobert
 SET ARRAY(322985)=""  ;"DFN TO IGNORE VanceNorman
 SET ARRAY(321662)=""  ;"DFN TO IGNORE HoeflerLonnie
 SET ARRAY(323247)=""  ;"DFN TO IGNORE VanceDianne
 SET ARRAY(324289)=""  ;"DFN TO IGNORE EmelDonald
 IF $D(ARRAY(TMGDFN)) SET RESULT=1
 QUIT RESULT
 ;"
PRNTG3XS()  ;"Print outstanding claims that have been submitted more than 3 times
 ;"
 IF $$OPENCHCK=0 QUIT   ;" DON'T RUN IF WE AREN'T OPEN
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$GT3XS_$$DATAFILETYPE
 SET LOGFILE=$$GT3XS_$$LOGFILETYPE
 ;"
 NEW TMGRESULT
 SET TMGRESULT=$$SQLREQUEST($$SUBGT3XS(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . DO MAKEALERT("ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2)) 
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2))
 ;"
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 ;1011815,324561,WALDEN,JACOB,1/23/1991,1/27/2025,90471,,,2.28,6,5/12/2025
 ;1011815,324561,WALDEN,JACOB,1/23/1991,1/27/2025,90656,,,30,6,5/12/2025
 NEW INIDX,OUTARRAY SET INIDX=0
 FOR  SET INIDX=$O(TMGARRAY(INIDX)) QUIT:INIDX'>0  DO
 . NEW ACCT,VISIT,NAME,DOB,DOS,CPT,BALANCE,SUBTIMES,LDT
 . SET NAME=$G(TMGARRAY(INIDX,3))_","_$G(TMGARRAY(INIDX,4))_" ("_$G(TMGARRAY(INIDX,5))_")"
 . SET DOS=$G(TMGARRAY(INIDX,6))_" "_$G(TMGARRAY(INIDX,2))_"V"_$G(TMGARRAY(INIDX,1))
 . SET OUTARRAY(NAME,DOS,$G(TMGARRAY(INIDX,7)))=$G(TMGARRAY(INIDX,10))_"^"_$G(TMGARRAY(INIDX,11))_"^"_$G(TMGARRAY(INIDX,12))
 ;"
 NEW %ZIS,IOP
 SET IOP="S121-LAUGHLIN-LASER"
 DO ^%ZIS  ;"standard device call
 IF POP QUIT
 USE IO
 ;"
 WRITE !
 WRITE "************************************************************",!
 WRITE "          Outstanding claims submitted more than 3 times",!
 WRITE "                     ",$$TODAY^TMGDATE(1),!
 WRITE "              Please deliver this report to EDDIE",!
 WRITE "************************************************************",!
 WRITE "                                            (From TMGSSQL1.m)",!!
 ;"
 NEW NAME SET NAME=""
 FOR  SET NAME=$O(OUTARRAY(NAME)) QUIT:NAME=""  DO
 . NEW DOS SET DOS=""
 . WRITE "*",NAME,!
 . FOR  SET DOS=$O(OUTARRAY(NAME,DOS)) QUIT:DOS=""  DO
 . . WRITE "   -- DOS: ",DOS,!
 . . NEW CPT SET CPT=0
 . . FOR  SET CPT=$O(OUTARRAY(NAME,DOS,CPT)) QUIT:CPT'>0  DO
 . . . NEW DATA SET DATA=$G(OUTARRAY(NAME,DOS,CPT))
 . . . WRITE "     -->",CPT," - Balance: $",$P(DATA,"^",1),". Submitted ",$P(DATA,"^",2)," times with last one on ",$P(DATA,"^",3),!
 . . WRITE !
 DO ^%ZISC  ;" Close the output device
 QUIT
 ;"
GTPHQ9DT()  ;"GET PHQ-9 DATA
 ;"THIS WILL GET THE LAST PHQ-9 DATA, WHERE THE INSURANCE COMPANY PAID AND STORE IN FM FILE
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$PHQ9F_$$DATAFILETYPE
 SET LOGFILE=$$PHQ9F_$$LOGFILETYPE
 ;"
 NEW TMGRESULT
 SET TMGRESULT=$$SQLREQUEST($$PHQ9PD(),$$WINDIR(),DATAFILE,1,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . DO MAKEALERT("ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2)) 
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH SCHEDULE STATUS SQL: "_$P(TMGRESULT,"^",2))
 ;"
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 DO PROCPHQD^TMGSSQL7(.TMGARRAY,LOGFILE)
 QUIT
 ;"
GETDATE(PROMPT)
 NEW SQLDATE,FMDATE,INDATE,VALID
 SET VALID=0,INDATE="-1"
 FOR  DO  QUIT:VALID=1
 . WRITE !,PROMPT," (MM/DD/YYYY): "
 . READ INDATE
 . IF INDATE="^" SET VALID=1 QUIT
 . SET VALID=$$VALIDDT(INDATE,.SQLDATE,.FMDATE)
 . IF VALID=1 QUIT
 . ELSE  WRITE "Invalid date format. Please try again.",!
 IF INDATE="^" QUIT "-1"
 SET INDATE=SQLDATE
 QUIT INDATE
 ;"
PGBILL()
 NEW SQLDATE,FMDATE,BDT,EDT
 SET BDT=$$GETDATE("Get beginning date")
 IF BDT="-1" QUIT
 SET EDT=$$GETDATE("Get ending date")
 IF EDT="-1" QUIT
 NEW DATAFILE,LOGFILE
 SET DATAFILE=$$RANDOMF_$$DATAFILETYPE
 SET LOGFILE=$$RANDOMF_$$LOGFILETYPE
 ;"
 WRITE !,"REQUESTING INFORMATION FROM SEQUEL",!
 SET TMGRESULT=$$SQLREQUEST($$PGBILSQL(BDT,EDT),$$WINDIR(),DATAFILE,0,LOGFILE)
 IF +TMGRESULT'=1 DO  QUIT
 . NEW ALRTRESULT
 . DO LOGLINE(LOGFILE,"!ERROR WITH SQL QUERY! "_$P(TMGRESULT,"^",2),0,0)
 . ;"DO MAKEALERT("ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 . ;"DO INFRMALT^TMGXQAL(.ALRTRESULT,150,"ERROR WITH ENCOUNTER SQL: "_$P(TMGRESULT,"^",2))
 DO LOGLINE(LOGFILE,"SQL QUERY SUCCESSFUL. NOW RETURNING",0,0)
 NEW FULLPNAME,TMGARRAY SET FULLPNAME=$$SQLDIRECTORY^TMGSSQL1()_DATAFILE
 DO LCSV2ARR^TMGIOUT4(FULLPNAME,"TMGARRAY")
 NEW RSLT SET RSLT=$$RMFILE^TMGKERNL($$SQLDIRECTORY()_DATAFILE)
 ;"
  NEW %ZIS,IOP
 SET IOP="S121-LAUGHLIN-LASER"
 DO ^%ZIS  ;"standard device call
 IF POP QUIT
 USE IO
 ;"
 WRITE !
 WRITE "************************************************************",!
 WRITE "          LAB CHARGES TO COMPARE TO PATHGROUP BILL",!
 WRITE "************************************************************",!
 WRITE "                                            (From TMGSSQL1.m)",!!
 ;"
 NEW BILLEDARR
 NEW IDX SET IDX=0
 FOR  SET IDX=$O(TMGARRAY(IDX)) QUIT:IDX'>0  DO
 . IF $G(TMGARRAY(IDX,5))="80305" QUIT
 . SET BILLEDARR($G(TMGARRAY(IDX,1))_","_$G(TMGARRAY(IDX,2)),$G(TMGARRAY(IDX,3)),$G(TMGARRAY(IDX,5)))=$G(TMGARRAY(IDX,4))
 NEW PTNAME SET PTNAME=""
 FOR  SET PTNAME=$O(BILLEDARR(PTNAME)) QUIT:PTNAME=""  DO
 . NEW DATE SET DATE=""
 . FOR  SET DATE=$O(BILLEDARR(PTNAME,DATE)) QUIT:DATE=""  DO
 . . WRITE DATE,?25,PTNAME
 . . NEW CPT SET CPT=""
 . . FOR  SET CPT=$O(BILLEDARR(PTNAME,DATE,CPT)) QUIT:CPT=""  DO
 . . . WRITE ?50,CPT,?75,$G(BILLEDARR(PTNAME,DATE,CPT)),!
 DO ^%ZISC  ;" Close the output device
 QUIT
 