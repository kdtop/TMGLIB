TMGRX005 ;TMG/kst/Patient medication code; 08/23/17
       ;;1.0;TMG-LIB;**1**;08/23/17
 ;       
 ;"Code for handling parsed medication array
 ;"~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--
 ;"Copyright (c) 09/10/17  Kevin S. Toppenberg MD
 ;"
 ;"This file is part of the TMG LIBRARY, and may only be used in accordence
 ;" to license terms outlined in separate file TMGLICNS.m, which should 
 ;" always be distributed with this file.
 ;"~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--
 ;
 ;"=======================================================================
 ;" API -- Public Functions.
 ;"=======================================================================
 ;
 ;"=======================================================================
 ;"PRIVATE API FUNCTIONS
 ;"=======================================================================
 ;"CHKREGRX(LINE,ABORT,TRAIN) -- Interactive test and registration of Rx. 
 ;"REGRX(ARR)  -- REGISTER A MEDICATION
 ;"REGSTRTH(ARR,IEN22733) -- Register strength
 ;"EDITRX(IEN22733)  -- Edit a med record
 ;"SELRX2 -- CHECK WHICH PARSES ARE GOOD. 
 ;"SCRNVAP(ARR)  -- DIC screening routine 
 ;"PKFRMIEN(IEN22733)  --PICK FORM SUB IEN FROM CURRENTLY REGISTERED SUBRECORDS.
 ;"PICKFORM(IEN22733,ALLOWNEW)  -- PICK FORM      
 ;"SHOWSTRT(IEN22733)  --SHOW CURRENTLY REGISTERED STRENGTHS FOR MEDICATION
 ;"EDTSTRNT(IEN22733)  -- EDIT CURRENT REGISTERED STRENGTHS FOR MEDICATION
 ;"SHOWMODS(IEN22733,NOPAUSE)  --SHOW CURRENT MODIFIERS FOR MEDICATION
 ;"ADDMOD(IEN22733)  -- ADD A MODIFIER FOR A MEDICATION  
 ;
 ;"=======================================================================
 ;"DEPENDENCIES
 ;"=======================================================================
 ;"Uses:  
 ;"=======================================================================
 ;   
CHKREGRX(LINE,ABORT,TRAIN) ;
  ;"NOTE: This is an interactive process, expecting user to be at command-line.
  NEW RESULT SET RESULT=""
  NEW FNRESULT SET FNRESULT=1
  NEW IEN22733 SET IEN22733=0
  NEW NEEDRX SET NEEDRX=1
  NEW NEEDSTRENGTH SET NEEDSTRENGTH=1
  NEW MEDNAME SET MEDNAME=""
  NEW USRPICK,MENU,IDX
  SET TRAIN=+$GET(TRAIN)
  NEW ARR
  IF $GET(LINE)="" GOTO CKR2B
CKR1 ;  
  KILL ARR DO PARSELN^TMGRX001(.ARR,LINE,.TRAIN)
CKR2 ;  
  WRITE !,"  ==================================================",!
  WRITE "Prossing medication entry:",!
  WRITE "  ",LINE,!
  SET IEN22733=+$GET(ARR("IEN22733"))
  SET NEEDRX=(IEN22733'>0)
  SET NEEDSTRENGTH=($ORDER(ARR("STRENGTH","IENS",""))="")
  IF ('NEEDRX)&('NEEDSTRENGTH) GOTO CKR3
CKR2B  ;  
  SET MEDNAME=$GET(ARR("MEDICATION","INPUT NAME")) 
  IF MEDNAME="" DO
  . IF IEN22733>0 SET MEDNAME=$PIECE($GET(^TMG(22733,IEN22733,0)),"^",1)
  . IF MEDNAME="" SET MEDNAME="this medication"
  IF NEEDRX WRITE !,"Medication doesn't appear to be registered in system",!
  IF (NEEDRX=0),NEEDSTRENGTH WRITE "Strength doesn't appear to be registered, or is missing from input",!
CKRM ;       
  NEW RXREF SET RXREF=$NAME(^TMP("MEDIEN2^TMGRX004",$J)) KILL @RXREF  ;"FORCE REFRESH OF MEDS AFTER EDITING ETC.
  SET IDX=0  
  KILL MENU SET MENU(IDX)="Select Option:"
  SET MENU(IDX,1)="Line: "_LINE
  SET MENU(IDX,2)="Processed output: "_$$EXTERNAL^TMGRX003(.ARR)
  IF NEEDRX SET IDX=IDX+1,MENU(IDX)="Register medication NOW"_$CHAR(9)_"REGRX"
  ELSE  IF NEEDSTRENGTH DO
  . SET IDX=IDX+1,MENU(IDX)="Register strength for "_MEDNAME_$CHAR(9)_"REGSTRTH"
  IF IEN22733>0 DO
  . SET IDX=IDX+1,MENU(IDX)="Show "_MEDNAME_"'s registered strengths"_$CHAR(9)_"SHOWSTRT"
  . SET IDX=IDX+1,MENU(IDX)="Edit "_MEDNAME_"'s registered strengths"_$CHAR(9)_"EDITSTRENGTHS"    
  . SET IDX=IDX+1,MENU(IDX)="Edit "_MEDNAME_"'s output settings"_$CHAR(9)_"EDIT-OUTPUT"
  . SET IDX=IDX+1,MENU(IDX)="Edit "_MEDNAME_"'s capitalization settings"_$CHAR(9)_"EDIT-CAPITALIZATION"
  . SET IDX=IDX+1,MENU(IDX)="Edit "_MEDNAME_"'s brands"_$CHAR(9)_"EDIT-BRANDS"
  . SET IDX=IDX+1,MENU(IDX)="Edit "_MEDNAME_" record"_$CHAR(9)_"EDIT^"_IEN22733
  . SET IDX=IDX+1,MENU(IDX)="Show "_MEDNAME_"'s modifiers"_$CHAR(9)_"SHOWMODIFIERS"
  . SET IDX=IDX+1,MENU(IDX)="Add modifier for "_MEDNAME_$CHAR(9)_"ADDMODIFIER"
  SET IDX=IDX+1,MENU(IDX)="Edit another record to add ALIAS (or misspelling)"_$CHAR(9)_"EDIT^OTHER"
  SET IDX=IDX+1,MENU(IDX)="ADD a NEW record for another medication"_$CHAR(9)_"EDIT^NEW"
  IF IEN22733>0 SET IDX=IDX+1,MENU(IDX)="Dump (examine) record for "_MEDNAME_$CHAR(9)_"DUMP^"_IEN22733
  SET IDX=IDX+1,MENU(IDX)="Dump (examine) another record"_$CHAR(9)_"DUMP"
  SET IDX=IDX+1,MENU(IDX)="DEBUG through parse process"_$CHAR(9)_"DEBUG"
  SET IDX=IDX+1,MENU(IDX)="Show array of parsed information"_$CHAR(9)_"SHOWPARSE"
  SET IDX=IDX+1,MENU(IDX)="Parse again"_$CHAR(9)_"REPARSE"
  SET IDX=IDX+1,MENU(IDX)="Accept current parse as best possible"_$CHAR(9)_"DONE"
  SET IDX=IDX+1,MENU(IDX)="Skip this entry"_$CHAR(9)_"SKIP"
  SET IDX=IDX+1,MENU(IDX)="DONE"_$CHAR(9)_"DONE"
  SET USRPICK=$$MENU^TMGUSRI2(.MENU,IDX)
  IF USRPICK="^" SET FNRESULT=-1 GOTO CKRDN
  IF USRPICK="DONE" GOTO CKRDN
  IF USRPICK="REPARSE" GOTO CKR2
  . KILL ARR DO PARSELN^TMGRX001(.ARR,LINE,1)
  IF USRPICK="DEBUG" DO  GOTO CKR2
  . KILL ARR NEW CODE SET CODE="DO PARSELN^TMGRX001(.ARR,LINE,1)"
  . DO DIRDEBUG^TMGIDE(CODE)
  IF USRPICK="REGRX" DO  GOTO CKR1
  . DO REGRX(.ARR)  ;"REGISTER A MEDICATION
  IF USRPICK="REGSTRTH" DO  GOTO:(RESULT'>0) CKRM GOTO CKR1
  . SET RESULT=$$REGSTRTH(.ARR,IEN22733)  ;"Register strength
  IF $PIECE(USRPICK,"^",1)="EDIT" DO  GOTO CKR1
  . NEW DIC,X,Y SET DIC=22733,DIC(0)="MAEQ"
  . SET Y=$PIECE(USRPICK,"^",2)
  . IF Y="NEW" SET DIC(0)=DIC(0)_"L"  ;"LAYGO
  . IF Y'>0 WRITE ! DO ^DIC QUIT:Y'>0
  . DO EDITRX(+Y) 
  IF USRPICK="SHOWSTRT" DO  GOTO CKR2
  . DO SHOWSTRT(IEN22733)
  IF USRPICK="EDITSTRENGTHS" DO  GOTO CKR2
  . DO EDTSTRNT(IEN22733)  ;"EDIT CURRENT REGISTERED STRENGTHS FOR MEDICATION
  IF USRPICK="EDIT-OUTPUT" DO  GOTO CKR2
  . DO EDITFLDS(IEN22733,"1.11:1.16")
  IF USRPICK="EDIT-CAPITALIZATION" DO  GOTO CKR2
  . DO EDITFLDS(IEN22733,"1.21:1.26")
  IF USRPICK="EDIT-BRANDS" DO  GOTO CKR2
  . DO EDITFLDS(IEN22733,"1")
  IF USRPICK="SHOWPARSE" DO  GOTO CKR2
  . DO ARRDUMP^TMGMISC3("ARR")
  . DO PRESS2GO^TMGUSRI2
  IF USRPICK="SHOWMODIFIERS" DO  GOTO CKR2
  . DO SHOWMODS(IEN22733)
  IF USRPICK="ADDMODIFIER" DO  GOTO CKR2
  . DO ADDMOD(IEN22733)  ;"ADD A MODIFIER FOR A MEDICATION  
  IF USRPICK="SKIP" GOTO CKRDN
  IF $PIECE(USRPICK,"^",1)="DUMP" DO  GOTO CKRM
  . NEW IENS SET IENS=$PIECE(USRPICK,"^",2) IF IENS'="" SET IENS=IENS_","
  . NEW OPTION SET OPTION("NO LOOP")=1
  . DO ASKDUMP^TMGDEBU3(22733,IENS,.OPTION)
  IF USRPICK="DONE" DO  GOTO CKR3
  GOTO CKRM
  ;
CKR3 ;  
  WRITE "PROCESSED VERSION:",!,"  ",$$EXTERNAL^TMGRX003(.ARR),!
  WRITE "--------------------------",!
CKRDN ;
  QUIT FNRESULT
  ;
REGRX(ARR)  ;"REGISTER A MEDICATION
  NEW ABORT SET ABORT=0
  NEW GENERIC WRITE "GENERIC NAME: " READ GENERIC:DTIME WRITE ! 
  IF GENERIC="^" GOTO REGRDN
  NEW IEN SET IEN=0
  NEW FMIDX FOR FMIDX="B","B2","BRAND" QUIT:(IEN>0)!ABORT  DO
  . SET IEN=$ORDER(^TMG(22733,FMIDX,GENERIC,0))
  . IF IEN>0 WRITE "'",GENERIC,"' ALREADY PRESENT, Found in ",FMIDX," index." SET ABORT=1
  IF ABORT GOTO REGRDN
  NEW TMGFDA,TMGIEN,TMGMSG
  SET TMGFDA(22733,"+1,",.01)=GENERIC
  SET TMGFDA(22733,"+1,",1.11)="GE"  ;"GENERIC NAME
  SET TMGFDA(22733,"+1,",1.12)="GBI" ;"GIVEN MATCHED BRAND
  SET TMGFDA(22733,"+1,",1.13)="GMI" ;"GIVEN MATCHED MODIFIER, IF PROVIDED
  SET TMGFDA(22733,"+1,",1.14)="GF"  ;"GIVEN FORM
  SET TMGFDA(22733,"+1,",1.15)="RDI" ;"REGISTERED STRENGTH, IF INPUT GIVEN
  SET TMGFDA(22733,"+1,",1.16)="RUI" ;"REGISTERED UNITS, IF UNITS IN INPUT
  SET TMGFDA(22733,"+1,",1.21)="LC"  ;"LOWER CASE
  SET TMGFDA(22733,"+1,",1.22)="FU"  ;"FIRST LETTER OF WORDS CAPPED
  SET TMGFDA(22733,"+1,",1.21)="LC"  ;"LOWER CASE
  SET TMGFDA(22733,"+1,",1.23)="LC"  ;"LOWER CASE
  SET TMGFDA(22733,"+1,",1.24)="LC"  ;"LOWER CASE
  SET TMGFDA(22733,"+1,",1.25)="LC"  ;"LOWER CASE
  SET TMGFDA(22733,"+1,",1.26)="LC"  ;"LOWER CASE  
  DO UPDATE^DIE("","TMGFDA","TMGIEN","TMGMSG")
  IF $DATA(TMGMSG("DIERR")) DO  GOTO:ABORT REGRDN
  . WRITE "ERROR: "_$$GETERRST^TMGDEBU2(.TMGMSG) 
  . SET ABORT=1
  SET IEN=$GET(TMGIEN(1)) IF IEN'>0 DO  GOTO REGRDN
  . WRITE "UNABLE TO FIND IEN OF ADDED FILE",! SET ABORT=1
  NEW BRAND WRITE "BRAND NAME (optional): " READ BRAND:DTIME WRITE !
  IF BRAND'="" DO
  . NEW IEN2 SET IEN2=0 FOR FMIDX="B","B2","BRAND" QUIT:(IEN2>0)!ABORT  DO
  . . SET IEN2=$ORDER(^TMG(22733,FMIDX,BRAND,0))
  . . IF IEN2>0 WRITE "'",BRAND,"' ALREADY PRESENT, Found in ",FMIDX," index." SET ABORT=1
  . IF ABORT QUIT
  . KILL TMGFDA,TMGIEN,TMGMSG
  . SET TMGFDA(22733.01,"+1,"_IEN_",",.01)=BRAND
  . DO UPDATE^DIE("","TMGFDA","TMGIEN","TMGMSG")
  . IF $DATA(TMGMSG("DIERR")) DO  QUIT:ABORT
  . . WRITE "ERROR: "_$$GETERRST^TMGDEBU2(.TMGMSG) 
  . . SET ABORT=1
  IF ABORT GOTO REGRDN
  WRITE !,"Added.  Now edit record to fill in any missing details.",!
  DO EDITRX(IEN) 
REGRDN  ;  
  QUIT
  ;
REGSTRTH(ARR,IEN22733)  ;"Register strength
  ;"RESULT: 1 if OK, 0 if not
  NEW LINE SET LINE=$GET(ARR("ORIG"))
  IF LINE'="" WRITE !,"Processing input line:",!,"  ",LINE,!
  NEW X,Y,DIC,RESULT SET (Y,RESULT)=0
  NEW IEN50D606  ;"<-- IEN in DOSAGE FORM (50.606)
  NEW IEN50D607  ;"<-- IEN in DRUG UNITS (50.607)
  NEW IEN50D68   ;"<-- VA PRODUCT (50.68) this is VA medication
  ;"-------------------------------------------
  NEW STRENGTH SET STRENGTH=$GET(ARR("STRENGTH"))
  IF STRENGTH="" DO  GOTO:STRENGTH="" REGDDN
  . WRITE "Enter strength (e.g. '20'), WITHOUT units (e.g. NOT '20 mg'). (^ to abort)",!
  . WRITE "STRENGTH: " READ STRENGTH:DTIME WRITE !
  IF STRENGTH["^" QUIT  ;"leaves result as 0 (abort)
  SET ARR("REG STRENGTH")=STRENGTH
  ;"----------------------------------------
  SET IEN50D607=+$GET(ARR("UNITS","IEN50.607")) 
  IF IEN50D607>0 GOTO REGD2
  SET %=1 WRITE "Use MG for units of strength" DO YN^DICN WRITE !
  IF %=-1 GOTO REGDDN    ;"leaves result as 0 (abort)
  IF %=1 DO
  . SET IEN50D607=+$ORDER(^PS(50.607,"B","MG",0))
  . IF IEN50D607'>0 WRITE "Sorry couldn't automatically look that up.",!
  IF IEN50D607'>0 DO
  . SET DIC=50.607,DIC(0)="MAEQ" 
  . SET DIC("A")="Select UNITS NAME (e.g. 'MG'): "
  . DO ^DIC WRITE !
  . SET IEN50D607=+Y
  IF IEN50D607'>0 GOTO REGDDN
REGD2 ;"--------------------------------------
  SET IEN50D606=$$PICKFORM(IEN22733)  ;"PICK FORM      
  IF IEN50D606'>0 GOTO REGDDN
  ;"-------------------------------------------
  WRITE "Pick VA drug & strength that matches:",! 
  KILL X,Y,DIC SET DIC=50.68,DIC("S")="IF $$SCRNVAP^TMGRX005(.ARR)",DIC(0)="MEQ"
  SET X=$GET(ARR("MEDICATION","GENERIC","DATABASE"))
  IF X="" SET X=$GET(ARR("MEDICATION","GENERIC"))
  IF X="" SET X=$PIECE($GET(^TMG(22733,IEN22733,0)),"^",1)
  IF X="" SET DIC(0)="MAEQ"
  ;"WRITE "X='",$GET(X),"'",!
  ;"WRITE "DIC(0)=",DIC(0),!
  DO ^DIC WRITE !
  SET IEN50D68=+Y
  IF IEN50D68>0 GOTO REGD3
  SET %=1 WRITE "No drug found / selected.  Try broader search" DO YN^DICN WRITE !
  IF %=-1 GOTO REGDDN
  IF %=2 GOTO REGD3
  KILL X,Y,DIC SET DIC=50.68,DIC(0)="MAEQ"
  DO ^DIC
  SET IEN50D68=+Y  
REGD3 ;"------------------------------------------- 
  NEW ABORT SET ABORT=0
  NEW SUBIEN SET SUBIEN=$ORDER(^TMG(22733,IEN22733,2,"B",IEN50D606,0)) ;"FIELD 2, 'DOSAGE FORM'
  IF SUBIEN'>0 DO  QUIT:ABORT
  . NEW TMGFDA,TMGIEN,TMGMSG
  . SET TMGFDA(22733.02,"+1,"_IEN22733_",",.01)=IEN50D606
  . DO UPDATE^DIE("","TMGFDA","TMGIEN","TMGMSG")
  . IF $DATA(TMGMSG("DIERR")) DO  QUIT
  . . WRITE "ERROR: "_$$GETERRST^TMGDEBU2(.TMGMSG)
  . . SET ABORT=1
  . SET SUBIEN=$GET(TMGIEN(1)) IF SUBIEN'>0 DO  QUIT
  . . WRITE "UNABLE TO FIND IEN OF ADDED FILE",!
  . . SET ABORT=1
  KILL TMGFDA,TMGIEN,TMGMSG
  SET TMGFDA(22733.03,"+1,"_SUBIEN_","_IEN22733_",",.01)=STRENGTH  ;"SUB-FIELD 1, 'STRENGTH'
  IF IEN50D607>0 SET TMGFDA(22733.03,"+1,"_SUBIEN_","_IEN22733_",",.02)=IEN50D607
  IF IEN50D68>0 SET TMGFDA(22733.03,"+1,"_SUBIEN_","_IEN22733_",",.03)=IEN50D68
  DO UPDATE^DIE("","TMGFDA","TMGIEN","TMGMSG")
  IF $DATA(TMGMSG("DIERR")) DO  GOTO REGDDN
  . WRITE "ERROR: "_$$GETERRST^TMGDEBU2(.TMGMSG)
  ;"----------------------------------------
  WRITE !,"Added.",!
  NEW MENU,USRPICK
REGDM1 ;
  KILL MENU
  SET MENU(0)="Select option"
  SET MENU(1)="Edit record to fill in any missing details"_$CHAR(9)_"EDIT"
  SET MENU(2)="Dump record to review"_$CHAR(9)_"DUMP"
  SET MENU(3)="DONE"_$CHAR(9)_"DONE"
  SET USRPICK=$$MENU^TMGUSRI2(.MENU,"^")
  IF (USRPICK="^")!(USRPICK="DONE") GOTO REGDDN
  IF USRPICK="EDIT" DO  GOTO REGDM1  
  . DO EDITRX(IEN22733)
  IF USRPICK="DUMP" DO  GOTO REGDM1
  . NEW OPTION SET OPTION("NO LOOP")=1
  . DO ASKDUMP^TMGDEBU3(22733,IEN22733_",",.OPTION)
  SET RESULT=1
  ;
REGDDN ;  
  QUIT
  ;
SCRNVAP(ARR)  ;"DIC screening routine 
  ;"INPUT: ARR -- PASSED BY REFERENCE.  The array as created by PARSELN^TMGRX001
  ;"uses Y in global scope as the IEN50.68 of the entry to be screened.
  SET RESULT=1  ;"if result=0 then option NOT shown
  NEW STRENGTH SET STRENGTH=$GET(ARR("REG STRENGTH"))
  NEW RX SET RX=$PIECE($GET(^PSNDF(50.68,+Y,0)),"^",1)
  SET RESULT=(RX[STRENGTH)
  QUIT RESULT
  ;
PKFRMIEN(IEN22733)  ;"PICK FORM SUB IEN FROM CURRENTLY REGISTERED SUBRECORDS.
  NEW RESULT SET RESULT=0
  NEW USRPICK,MENU,IDX,SUBIEN,TABIEN
PFRI1  ;  
  KILL MENU SET IDX=0,TABIEN=0
  SET MENU(IDX)="Pick FORM for "_$PIECE($GET(^TMG(22733,IEN22733,0)),"^",1)
  SET SUBIEN=0
  FOR  SET SUBIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN)) QUIT:SUBIEN'>0  DO
  . SET IEN50D606=$PIECE($GET(^TMG(22733,IEN22733,2,SUBIEN,0)),"^",1)
  . NEW FORMNAME SET FORMNAME=$PIECE($GET(^PS(50.606,IEN50D606,0)),"^",1)
  . QUIT:FORMNAME=""
  . IF FORMNAME="TAB" SET TABIEN=IEN50D606
  . SET IDX=IDX+1,MENU(IDX)="Form '"_FORMNAME_"'"_$CHAR(9)_SUBIEN
  IF IDX=0 DO  GOTO PFRIDN
  . WRITE "Sorry, this record does not have any registered strength forms to pick.",!
  SET USRPICK=$$MENU^TMGUSRI2(.MENU,"^")  
  IF USRPICK="^" GOTO PFRIDN
  SET RESULT=+USRPICK
  IF RESULT>0 GOTO PFRIDN
  GOTO PFRI1  
PFRIDN ;
  QUIT RESULT
  ;          
PICKFORM(IEN22733,ALLOWNEW)  ;"PICK FORM      
  NEW IEN50D606 SET IEN50D606=0
  NEW USRPICK,MENU,IDX,SUBIEN,TABIEN
  SET ALLOWNEW=$GET(ALLOWNEW,1)
PFL1  ;  
  KILL MENU SET IDX=0,TABIEN=0
  SET MENU(IDX)="Pick FORM to use for "_$PIECE($GET(^TMG(22733,IEN22733,0)),"^",1)
  SET SUBIEN=0
  FOR  SET SUBIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN)) QUIT:SUBIEN'>0  DO
  . SET IEN50D606=$PIECE($GET(^TMG(22733,IEN22733,2,SUBIEN,0)),"^",1)
  . NEW FORMNAME SET FORMNAME=$PIECE($GET(^PS(50.606,IEN50D606,0)),"^",1)
  . QUIT:FORMNAME=""
  . IF FORMNAME="TAB" SET TABIEN=IEN50D606
  . SET IDX=IDX+1,MENU(IDX)="Use form '"_FORMNAME_"'"_$CHAR(9)_IEN50D606
  IF TABIEN'>0 SET IDX=IDX+1,MENU(IDX)="Use form 'TAB'"_$CHAR(9)_"TAB"  
  IF ALLOWNEW SET IDX=IDX+1,MENU(IDX)="MANUALLY Select form"_$CHAR(9)_"NEW"
  ;
  SET USRPICK=$$MENU^TMGUSRI2(.MENU,"^")
  IF USRPICK="^" GOTO PFDN
  IF USRPICK="TAB" DO  GOTO:(IEN50D606>0) PFDN  
  . SET IEN50D606=$ORDER(^PS(50.606,"B","TAB",0))
  . IF IEN50D606'>0 WRITE "Sorry couldn't automatically look that up.",!
  IF USRPICK="NEW" DO  GOTO:(IEN50D606>0) PFDN  
  . SET DIC=50.606,DIC(0)="MAEQ" 
  . SET DIC("A")="Select DOSAGE FORM NAME (e.g. 'TAB'): "
  . DO ^DIC WRITE !
  . SET IEN50D606=+Y
  IF USRPICK>0 DO  GOTO:(IEN50D606>0) PFDN  
  . SET IEN50D606=USRPICK
  GOTO PFL1
  ;
PFDN  ;  
  QUIT IEN50D606
  ;
SHOWSTRT(IEN22733)  ;"SHOW CURRENTLY REGISTERED STRENGTHS FOR MEDICATION
  WRITE $PIECE($GET(^TMG(22733,IEN22733,0)),"^",1),!
  NEW SUBIEN SET SUBIEN=0
  FOR  SET SUBIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN)) QUIT:SUBIEN'>0  DO
  . NEW IEN50D606 SET IEN50D606=$PIECE($GET(^TMG(22733,IEN22733,2,SUBIEN,0)),"^",1)
  . NEW FORMNAME SET FORMNAME=$PIECE($GET(^PS(50.606,IEN50D606,0)),"^",1) QUIT:FORMNAME=""
  . WRITE FORMNAME,!
  . NEW STRIEN SET STRIEN=0
  . FOR  SET STRIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN,1,STRIEN)) QUIT:STRIEN'>0  DO
  . . NEW ZN SET ZN=$GET(^TMG(22733,IEN22733,2,SUBIEN,1,STRIEN,0)) QUIT:ZN=""
  . . NEW STRENGTH SET STRENGTH=$PIECE(ZN,"^",1)
  . . NEW UNITS SET UNITS=$PIECE($GET(^PS(50.607,+$PIECE(ZN,"^",2),0)),"^",1)
  . . NEW IEN50D68 SET IEN50D68=+$PIECE(ZN,"^",3)
  . . NEW VAPROD SET VAPROD=$PIECE($GET(^PSNDF(50.68,IEN50D68,0)),"^",1)
  . . NEW OTC SET OTC=$$GET1^DIQ(22733.03,STRIEN_","_SUBIEN_","_IEN22733_",",.04)  
  . . WRITE "  ",STRENGTH," ",UNITS
  . . IF OTC'="" WRITE "  OTC=",OTC
  . . WRITE !,"     --> VA RX: ",VAPROD,!
  . . NEW ALIASIEN SET ALIASIEN=0
  . . FOR  SET ALIASIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN,1,STRIEN,1,ALIASIEN)) QUIT:ALIASIEN'>0  DO
  . . . NEW ZN SET ZN=$GET(^TMG(22733,IEN22733,2,SUBIEN,1,STRIEN,1,ALIASIEN,0)) QUIT:ZN=""
  . . . WRITE "     Alt strength: ",$PIECE(ZN,"^",1),!
  DO PRESS2GO^TMGUSRI2
  QUIT  
  ;
EDTSTRNT(IEN22733)  ;"EDIT CURRENT REGISTERED STRENGTHS FOR MEDICATION
  NEW USRPICK,MENU,IDX,SUBIEN,TABIEN,IEN50D606,FORMNAME
  NEW SUBIEN SET SUBIEN=$$PKFRMIEN(IEN22733) IF SUBIEN'>0 QUIT
  NEW RXNAME SET RXNAME=$PIECE($GET(^TMG(22733,IEN22733,0)),"^",1)
  SET IEN50D606=$PIECE($GET(^TMG(22733,IEN22733,2,SUBIEN,0)),"^",1)
  SET FORMNAME=$PIECE($GET(^PS(50.606,IEN50D606,0)),"^",1) QUIT:FORMNAME=""
EDS1  ;  
  KILL MENU SET IDX=0,TABIEN=0
  SET MENU(IDX)="Pick STRENGTH for "_$PIECE($GET(^TMG(22733,IEN22733,0)),"^",1)      
  NEW STRIEN SET STRIEN=0
  FOR  SET STRIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN,1,STRIEN)) QUIT:STRIEN'>0  DO
  . NEW ZN SET ZN=$GET(^TMG(22733,IEN22733,2,SUBIEN,1,STRIEN,0)) QUIT:ZN=""
  . NEW STRENGTH SET STRENGTH=$PIECE(ZN,"^",1)
  . NEW UNITS SET UNITS=$PIECE($GET(^PS(50.607,+$PIECE(ZN,"^",2),0)),"^",1)
  . NEW IEN50D68 SET IEN50D68=+$PIECE(ZN,"^",3)
  . NEW VAPROD SET VAPROD=$PIECE($GET(^PSNDF(50.68,IEN50D68,0)),"^",1)
  . NEW OTC SET OTC=$$GET1^DIQ(22733.03,STRIEN_","_SUBIEN_","_IEN22733_",",.04)  
  . NEW SHOW SET SHOW=STRENGTH_" "_UNITS 
  . IF OTC'="" SET SHOW=SHOW_"  OTC="_OTC
  . SET IDX=IDX+1,MENU(IDX)="Strength: "_SHOW_$CHAR(9)_STRIEN    
  IF IDX=0 DO  GOTO EDSDN
  . WRITE "Sorry, this drug FORM does not have any registered strengths to pick.",!
  SET USRPICK=$$MENU^TMGUSRI2(.MENU,"^")  
  IF USRPICK="^" GOTO EDSDN
  SET STRIEN=+USRPICK
  IF STRIEN>0 GOTO EDS2
  GOTO EDS1  
EDS2 ;
  NEW DIE,DA,DR 
  SET DIE=$NAME(^TMG(22733,IEN22733,2,SUBIEN,1))
  SET DIE=$$OREF^DILF(DIE)
  SET DA=STRIEN,DA(1)=SUBIEN,DA(2)=IEN22733
  SET DR=".01:9999"
  IF 1=1 DO
  . WRITE "NOTE: DIE=",DIE,!
  . WRITE "      DA:",!
  . ZWR DA  
  . WRITE "      DR=",DR,!
  . WRITE "CALLING ^DIE",!
  DO ^DIE WRITE !
EDSDN ;
  QUIT
  ;
SHOWMODS(IEN22733,NOPAUSE)  ;"SHOW CURRENT MODIFIERS FOR MEDICATION
  WRITE $PIECE($GET(^TMG(22733,IEN22733,0)),"^",1),!
  SET NOPAUSE=+$GET(NOPAUSE)
  NEW SUBIEN SET SUBIEN=0
  FOR  SET SUBIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN)) QUIT:SUBIEN'>0  DO
  . SET IEN50D606=$PIECE($GET(^TMG(22733,IEN22733,2,SUBIEN,0)),"^",1)
  . NEW FORMNAME SET FORMNAME=$PIECE($GET(^PS(50.606,IEN50D606,0)),"^",1) QUIT:FORMNAME=""
  . WRITE "  ",FORMNAME,!
  . NEW MODIEN SET MODIEN=0
  . FOR  SET MODIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN,2,MODIEN)) QUIT:MODIEN'>0  DO
  . . NEW ZN SET ZN=$GET(^TMG(22733,IEN22733,2,SUBIEN,2,MODIEN,0)) QUIT:ZN=""
  . . WRITE "    MODIFIER: ",$PIECE(ZN,"^",1)
  . . IF $PIECE(ZN,"^",2)="Y" WRITE "  <-- PREFERRED",!
  . . ELSE  WRITE !  
  IF 'NOPAUSE DO PRESS2GO^TMGUSRI2
  QUIT  
  ;
ADDMOD(IEN22733)  ;"ADD A MODIFIER FOR A MEDICATION  
  DO SHOWMODS(IEN22733,1)  ;"SHOW CURRENT MODIFIERS FOR MEDICATION
  SET IEN50D606=$$PICKFORM(IEN22733,0)  ;"PICK FORM      
  IF IEN50D606'>0 GOTO AMDN
  NEW FOUNDIEN SET FOUNDIEN=0
  NEW SUBIEN SET SUBIEN=0
  FOR  SET SUBIEN=$ORDER(^TMG(22733,IEN22733,2,SUBIEN)) QUIT:(SUBIEN'>0)!(FOUNDIEN>0)  DO
  . IF $PIECE($GET(^TMG(22733,IEN22733,2,SUBIEN,0)),"^",1)'=IEN50D606 QUIT
  . SET FOUNDIEN=SUBIEN
  IF FOUNDIEN'>0 DO  GOTO AMDN
  . WRITE "UNABLE TO FIND CHOSEN FORM",!
  . DO PRESS2GO^TMGUSRI2  
  WRITE "Enter name of new modifier (^ to abort): "
  NEW MOD READ MOD:$GET(DTIME,3600) WRITE !
  IF MOD["^" GOTO AMDN
  SET %=2 WRITE "Should modifier '"_MOD_"' be marked as PREFERRED" DO YN^DICN WRITE !
  IF %=-1 GOTO AMDN
  NEW TMGFDA,TMGIEN,TMGMSG  
  SET TMGFDA(22733.22,"+1,"_FOUNDIEN_","_IEN22733_",",.01)=MOD  
  IF %=1 SET TMGFDA(22733.22,"+1,"_FOUNDIEN_","_IEN22733_",",.02)="Y"
  DO UPDATE^DIE("","TMGFDA","TMGIEN","TMGMSG")
  IF $DATA(TMGMSG("DIERR")) DO  GOTO AMDN
  . WRITE "ERROR: "_$$GETERRST^TMGDEBU2(.TMGMSG)
AMDN  ;
  QUIT
  ; 
EDITRX(IEN22733)  ;
  DO EDITFLDS(IEN22733,".01:9999")
  QUIT
  ;
EDITFLDS(IEN22733,FLDS)  ;"EDIT FIELD(S)
  NEW DIE,DA,DR SET DIE=22733,DA=IEN22733,DR=FLDS
  DO ^DIE WRITE !
  QUIT
  ;
