TMGCNSLT ;ELH - Consult Routines; 4/30/2014
	;;1.0;CONSULT/REQUEST TRACKING;2/18/14;Build 7
 ;
 ;"~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--
 ;"Copyright (c) 6/23/2015  Kevin S. Toppenberg MD
 ;"
 ;"This file is part of the TMG LIBRARY, and may only be used in accordence
 ;" to license terms outlined in separate file TMGLICNS.m, which should 
 ;" always be distributed with this file.
 ;"~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--~--
 ;
HDR	; Header for consult - wedge
	;
	;get and format eligibility info
	;D BLD("HDR",1,1,0,"YEAH BOY")
	;Q
	N VAEL,VAPA,GMRCPEL,SUB,GMRCFROM
	N CVELIG,CVMARKER ;WAT
	D ELIG^VADPT
	D ADD^VADPT
	S GMRCPEL=$P(VAEL(1),U,2)
	;
	F SUB=0 D
	.N GMRCFLN
	.S GMRCFLN=$P($G(^DPT(GMRCDFN,0)),U,1)
	.;S CVELIG=$$CVEDT^DGCV(GMRCDFN) S:$P($G(CVELIG),U,3) CVELIG="CV ELIGIBLE" ;WAT
	.D BLD("HDR",SUB,1,0,$G(GMRCDVL))
	.D BLD("HDR",SUB,1,6,"MEDICAL RECORD")
	.D BLD("HDR",SUB,0,39,"|")
	.D BLD("HDR",SUB,0,45,"CONSULTATION REQUEST")
	.D BLD("HDR",SUB,1,0,$G(GMRCDVL))
	.D BLD("HDR",SUB,1,0,$G(GMRCFLN))
	.;D BLD("HDR",SUB,0,45,$G(GMRCPEL))
	.;ELH commented 6/26/18 D BLD("HDR",SUB,1,0,$G(GMRCSN))
	.;ELH changed to below lineD BLD("HDR",SUB,0,16,$$EXDT(GMRCDOB))
        .D BLD("HDR",SUB,1,0,$$EXDT(GMRCDOB))
	.;D BLD("HDR",SUB,0,45,$G(GMRCELIG))
	.;D:$G(CVELIG)["CV" BLD("HDR",SUB,1,45,$G(CVELIG))
	;
	;                                  ADDRESS LINES 1-3
	F GMRCX=1,2,3 D:$L(VAPA(GMRCX))
	.D BLD("HDR",0,1,0,VAPA(GMRCX))
	.;I GMRCX=1 D BLD("HDR",0,0,51,"Standard Form 513 (Rev 9-77)")
	;
	;         CITY              STATE                ZIP CODE
	S GMRCX=VAPA(4)_"   "_$P(VAPA(5),U,2)_"      "_VAPA(6)
	;
	I $L(VAPA(8)) S GMRCX=GMRCX_"      Phone: "_VAPA(8)   ; TELEPHONE (IF AVAILABLE)
	D BLD("HDR",SUB,2,0,$G(GMRCDVL))
	D BLD("HDR",SUB,3,0," ")
	;
	Q
	;
FTR(GMRCSG)	;Footer for consult, wedge
	;
	N GMRCRMBD,GMRCFAC1,GMRCLOC,GMRCX,SUB,VAIN,VAPA,VAERR
	;
	D ADD^VADPT,INP^VADPT
	;
	S (GMRCLOC,GMRCRMBD)=""
	S GMRCLOC=$P($G(VAIN(4)),U,2)
	S GMRCRMBD=$G(VAIN(5))
	S:'$L(GMRCLOC) GMRCLOC=$P($G(^SC(+$P($G(^GMR(123,+GMRCIFN,0)),U,4),0)),U,1)
	;No location, IFC - consulting site
	I '$L(GMRCLOC),$P(GMRCRD,U,23),$P($G(GMRCRD(12)),U,5)="F" D
	.I $P(GMRCRD,U,21) S GMRCLOC=$$GET1^DIQ(4,$P(GMRCRD,U,21),.01)
	.E  S GMRCLOC=$$GET1^DIQ(4,$P(GMRCRD,U,23),.01)
	S:'$L(GMRCLOC) GMRCLOC=GMRCUL
	;
	
	D BLD("FTR",0,1,0,$G(GMRCEQL))
	D BLD("FTR",1,1,0,$G(GMRCEQL))
	;
	I ($G(GMRCSG("GMRCSIGM"))="electronic") D  I 1
	.;D BLD("FTR",0,1,0,"SIGNATURE & TITLE: ")
	.;D BLD("FTR",0,0,20,$G(GMRCSG("GMRCSIG"))_" /es/")
	.;D BLD("FTR",0,0,54,"|")
	.D BLD("FTR",0,1,20,$G(GMRCSG("GMRCSIGT")))
	.;D BLD("FTR",0,0,54,"|DATE: "_$$EXDT($G(GMRCSG("GMRCSDT"))))
	E  D
	.;D BLD("FTR",0,1,0,"AUTHOR & TITLE: ")
	.D BLD("FTR",0,0,20,$G(GMRCSG("GMRCSIG")))
	.;D BLD("FTR",0,0,54,"|")
	.D BLD("FTR",0,1,20,$G(GMRCSG("GMRCSIGT")))
	.;D BLD("FTR",0,0,54,"|DATE: "_$$EXDT($G(GMRCSG("GMRCSDT"))))
	Q
	;
BLD(SUB,NDX,LINE,TAB,TEXT,RUNTIME)  ;FOR FOOTER
        DO BLD^GMRCP5B(.SUB,.NDX,.LINE,.TAB,.TEXT,.RUNTIME)
        QUIT
        ;
EXDT(X)   ;
        IF +X=X DO
        . DO EXDT^GMRCP5B(.X)
        QUIT X
        ;
ASSMBL(PAGELEN,PAGEWID) ;Original code in GMRCP5C
        ;This is called from PRNT^GMRCP5A. Used to 
	;format second page.
        N GMRCHDR,GMRCPG,SUB,GMRCPAGE,GMRCDVL
        ;
        S GMRCDVL="",$P(GMRCDVL,"-",PAGEWID+1)=""
        S ^TMP("GMRC",$J,"SF513")=$G(PAGELEN)
        S GMRCPG=1,GMRCHDR=""
        D CLRZONE(0)
        ;"D MERGE("HDR",0,1)
        ;"D MERGE("FTR",0,5)
        ;
        ;REQ    add reason for request segment
        ;
        D FORMAT("REQ",PAGELEN,PAGEWID,2)
        ;
        ;PDIAG  add provisional diagnosis segment
        ;
        ;D FORMAT("PDIAG",PAGELEN,PAGEWID,$$SIZE("PDIAG",1)+1)
        ;
        ;RES    add tiu results segment
        ;
        D FORMAT("RES",PAGELEN,PAGEWID,6)
        ;
        ;ADD    add addendum segment
        ;
        D FORMAT("ADD",PAGELEN,PAGEWID,4)
        ;
        ;SREP   add service report segment
        ;
        D FORMAT("SREP",PAGELEN,PAGEWID,5)
        ;
        ;COM    add comments segment
        ;
        D FORMAT("COM",PAGELEN,PAGEWID,5)
        ;
        I $D(GMRCPAGE(300000)) D OUTPUT(GMRCPG)
        ;
        Q
        ;
FORMAT(SUB,PAGELEN,PAGEWID,OFFSET) ;
        DO FORMAT^GMRCP5C(.SUB,.PAGELEN,.PAGEWID,.OFFSET)
        QUIT
        ;
MERGE(SUB,NDX,ZONE) ;
        DO MERGE^GMRCP5C(.SUB,.NDX,.ZONE)
        QUIT
	;
CLRZONE(ZONE) ;
        DO CLRZONE^GMRCP5C(.ZONE)
        QUIT
        ;
OUTPUT(GMRCPG) ;
        DO OUTPUT^GMRCP5C(.GMRCPG)
        QUIT
	;
CNSLTLNK(RESULT,IEN123,IEN8925,COMPLETE)  ;
        ;"Purpose: To link a consult with a TIU Note and complete (if
        ;"         requested)
        ;"Input: RESULT - Return variable
        ;"       IEN123 - IEN of consult
        ;"       IEN8925 - IEN of progress note
        ;"       COMPLETE - Boolean whether to complete (1 OR 0) DEFAULT IS 0
        NEW TMGFDA,TMGMSG,TMGIEN
        SET RESULT="1^SUCCESS"
        SET IEN123=+$GET(IEN123)
        IF IEN123'>0 SET RESULT="-1^NO CONSULT SENT" GOTO CLDN
        SET IEN8925=+$GET(IEN8925)
        IF IEN8925'>0 SET RESULT="-1^NO TIU NOTE SENT" GOTO CLDN
        SET COMPLETE=+$GET(COMPLETE)      
        SET TMGFDA(123.03,"+1,"_IEN123_",",.01)=IEN8925_";TIU(8925,"
        DO UPDATE^DIE("","TMGFDA","TMGIEN","TMGMSG")
        IF $DATA(TMGMSG("DIERR")) DO  GOTO CLDN
        . SET RESULT="-1^"_$$GETERRST^TMGDEBU2(.TMGMSG)
        IF COMPLETE=0 GOTO CLDN
        KILL TMGFDA,TMGMSG,TMGIEN
        SET TMGFDA(123,IEN123_",",8)="COMPLETE"
        DO FILE^DIE("E","TMGFDA","TMGMSG")
        IF $DATA(TMGMSG("DIERR")) SET RESULT="-1^"_$$GETERRST^TMGDEBU2(.TMGMSG) GOTO CLDN
        ;"complete order
        NEW IEN100 SET IEN100=+$P($G(^GMR(123,IEN123,0)),"^",3)
        IF IEN100'>0 GOTO CLDN
        KILL TMGFDA
        SET TMGFDA(100,IEN100_",",5)="COMPLETE"
        DO FILE^DIE("E","TMGFDA","TMGMSG")
        IF $DATA(TMGMSG("DIERR")) SET RESULT="-1^"_$$GETERRST^TMGDEBU2(.TMGMSG) GOTO CLDN
CLDN    QUIT
        ;"
COMPORDR()  ;"
        ;"Purpose: To look at each consult, and if it is Completed... set
        ;"         the corresponding order to complete
        NEW COMPLIEN SET COMPLIEN=+$ORDER(^ORD(100.01,"B","COMPLETE",0))
        IF COMPLIEN'>0 WRITE "COMPLETED IEN NOT FOUND IN FILE 100.01",! GOTO CODN
        NEW IEN123 SET IEN123=0
        FOR  SET IEN123=$ORDER(^GMR(123,IEN123)) QUIT:+IEN123'>0  DO
        . IF +$P($G(^GMR(123,IEN123,0)),"^",12)'=COMPLIEN QUIT
        . WRITE "CONSULT ",IEN123," IS COMPLETED",!
        . NEW IEN100 SET IEN100=+$P($G(^GMR(123,IEN123,0)),"^",3)
        . IF IEN100'>0 QUIT
        . NEW STATUS SET STATUS=$P($G(^OR(100,IEN100,3)),"^",3)
        . IF STATUS=COMPLIEN QUIT
        . WRITE "          COMPLETING ",IEN100,!
        . NEW TMGFDA,TMGMSG
        . SET TMGFDA(100,IEN100_",",5)="COMPLETE"
        . DO FILE^DIE("E","TMGFDA","TMGMSG")
        . IF $DATA(TMGMSG("DIERR")) WRITE "-1^"_$$GETERRST^TMGDEBU2(.TMGMSG)
CODN    QUIT
        ;"
GTCONSLT(OUT)   ;"Gather all consults data from the TMG_TO TIU Fields
 NEW TMPLNAME SET TMPLNAME=""
 ;"^TIU(8927.1,"B","TMG_TO_CONSULT_ALLERGY",72)=""
 FOR  SET TMPLNAME=$O(^TIU(8927.1,"B",TMPLNAME)) QUIT:TMPLNAME=""  DO
 . IF TMPLNAME'["TMG_TO_CONSULT" QUIT
 . NEW SPECIALTY SET SPECIALTY=$P(TMPLNAME,"_",4)
 . IF SPECIALTY="" QUIT
 . NEW TMPLIEN SET TMPLIEN=$O(^TIU(8927.1,"B",TMPLNAME,0))
 . NEW SUBIEN SET SUBIEN=0
 . FOR  SET SUBIEN=$O(^TIU(8927.1,TMPLIEN,10,SUBIEN)) QUIT:SUBIEN'>0  DO
 . . NEW LINE SET LINE=$$UP^XLFSTR($G(^TIU(8927.1,TMPLIEN,10,SUBIEN,0)))
 . . IF LINE'["FAX" QUIT
 . . NEW OFFICE,FAX,PHONE
 . . SET OFFICE=$$TRIM^XLFSTR($P(LINE,"FAX",1)),LINE=$P(LINE,"FAX:",2)
 . . SET FAX=$$TRIM^XLFSTR($P(LINE,"PHONE",1))
 . . SET PHONE=$$TRIM^XLFSTR($P(LINE,"PHONE:",2))
 . . SET OUT(SPECIALTY,OFFICE)=PHONE_"^"_FAX
 QUIT
 ;"
CNSLTRPT(ROOT,TMGDFN,ID,ALPHA,OMEGA,DTRANGE,REMOTE,MAX,ORFHIE)  ;"HTML CONSULTANT REPORT
  ;"Purpose: Entry point, as called from CPRS REPORT system
  ;"Input: ROOT -- Pass by NAME.  This is where output goes
  ;"       TMGDFN -- Patient DFN ; ICN for foriegn sites
  ;"       ID --
  ;"       ALPHA -- Start date (lieu of DTRANGE)
  ;"       OMEGA -- End date (lieu of DTRANGE)
  ;"       DTRANGE -- # days back from today
  ;"       REMOTE --
  ;"       MAX    --
  ;"       ORFHIE -D-
  NEW HD SET HD="<TABLE BORDER=3><CAPTION><B>CONSULTANT CONTACT REPORT</B><BR>*Highlighted offices are associated with this patient's care.<BR>"
  SET HD=HD_"<TR><TH>OFFICE</TH><TH>PHONE NUMBER</TH><TH>FAX NUMBER</TH><TH>PHYSICIANS</TH></TR>"
  NEW TMGRESULT,CONSULTARR
  NEW OUTIDX SET OUTIDX=0
  DO GTCONSLT(.CONSULTARR)
  ;"
  NEW FAXES DO RECNTFAX^TMGTIUPR(.FAXES,TMGDFN)
  NEW FIDX SET FIDX=0
  FOR  SET FIDX=$O(FAXES(FIDX)) QUIT:FIDX'>0  DO
  . SET FAXES(FIDX)=$TR($P($G(FAXES(FIDX))," = ",2),"-")
  ;"
  NEW SPECIALTY SET SPECIALTY=""
  FOR  SET SPECIALTY=$O(CONSULTARR(SPECIALTY)) QUIT:SPECIALTY=""  DO
  . NEW OFFICE SET OFFICE=""
  . FOR  SET OFFICE=$O(CONSULTARR(SPECIALTY,OFFICE)) QUIT:OFFICE=""  DO
  . . NEW LINE,PHONE,FAX,PHYSICIANS
  . . SET LINE=$G(CONSULTARR(SPECIALTY,OFFICE)),PHONE=$P(LINE,"^",1),FAX=$P(LINE,"^",2)
  . . SET PHYSICIANS=$$GETDOCS(SPECIALTY,OFFICE,PHONE,FAX)
  . . SET TMGRESULT($I(OUTIDX))=SPECIALTY_"^"_OFFICE_"^"_PHONE_"^"_FAX_"^"_PHYSICIANS
  DO SETHTML2(.ROOT,.TMGRESULT,"CONSULTANT CONTACT REPORT",HD,5,.FAXES)
 QUIT
 ;"
GETDOCS(SPECIALTY,OFFICE,PHONE,FAX)
 NEW TMGRESULT SET TMGRESULT=""
 NEW SPECIEN SET SPECIEN=+$O(^TMG(22762,"B",SPECIALTY,0))
 IF SPECIEN'>0 SET SPECIEN=$$ADDSPEC(SPECIALTY)
 IF SPECIEN'>0 QUIT  ;" COULDN'T CREATE FOR SOME REASON
 NEW OFFICEIEN SET OFFICEIEN=+$O(^TMG(22762,SPECIEN,1,"B",OFFICE,0))
 IF OFFICEIEN'>0 DO ADDOFFICE(SPECIEN,OFFICE,PHONE,FAX) GOTO GDDN
 ;"
 NEW DOC SET DOC=""
 FOR  SET DOC=$O(^TMG(22762,SPECIEN,1,OFFICEIEN,1,"B",DOC)) QUIT:DOC=""  DO
 . IF TMGRESULT'="" SET TMGRESULT=TMGRESULT_","
 . SET TMGRESULT=TMGRESULT_DOC
GDDN
 QUIT TMGRESULT
 ;"
ADDSPEC(SPECIALTY)  ;"Add specialty
 NEW TMGRESULT SET TMGRESULT=0
 NEW TMGFDA,TMGERR,NEWIEN
 S TMGFDA(22762,"+1,",.01)=SPECIALTY  ; .01 = Specialty Field    
 D UPDATE^DIE("","TMGFDA","NEWIEN","TMGERR")
 SET TMGRESULT=+$G(NEWIEN(1))
 QUIT TMGRESULT
 ;"
ADDOFFICE(SPECIEN,OFFICENAME,PHONE,FAX)  ;"Add office
 NEW TMGRESULT SET TMGRESULT=0
 NEW TMGFDA,TMGERR,NEWIEN
 S TMGFDA(22762.01,"+1,"_SPECIEN_",",.01)=OFFICENAME  ; .01 = Office Name
 S TMGFDA(22762.01,"+1,"_SPECIEN_",",.02)=PHONE  ; .02 = Phone
 S TMGFDA(22762.01,"+1,"_SPECIEN_",",.03)=FAX  ; .03 = Fax
 D UPDATE^DIE("","TMGFDA","NEWIEN","TMGERR") 
 SET TMGRESULT=+$G(NEWIEN(1))
 QUIT TMGRESULT
 ;"
CONSCHAN(TMGRESULT,COMMANDSTR)  ;"RPC:TMG CPRS CONSULTANT CHANNEL TO GET CONSULTANT INFO, SET INFO, OR REMOVE INFO
 SET TMGRESULT(0)="1^SUCCESSFUL"
 NEW COMMAND SET COMMAND=$P(COMMANDSTR,"^",1)
 SET COMMANDSTR=$P(COMMANDSTR,"^",2,999)
 GOTO @COMMAND
 ;"
GETSPECS 
 NEW OUTIDX SET OUTIDX=0
 NEW IDX SET IDX=0
 FOR  SET IDX=$O(^TMG(22762,IDX)) QUIT:IDX'>0  DO
 . SET TMGRESULT($I(OUTIDX))=IDX_"^"_$G(^TMG(22762,IDX,0))
 QUIT
 ;"
GETOFFICES
 ;"COMMANDSTR SHOULD BE IDX OF SPECIALTY
 NEW OUTIDX SET OUTIDX=0
 NEW IDX SET IDX=0
 FOR  SET IDX=$O(^TMG(22762,COMMANDSTR,1,IDX)) QUIT:IDX'>0  DO
 . SET TMGRESULT($I(OUTIDX))=IDX_"^"_$G(^TMG(22762,COMMANDSTR,1,IDX,0))  ;"_", Phone: "_$P($G(^TMG(22762,COMMANDSTR,1,IDX,2)),"^",1)_", Fax: "_$P($G(^TMG(22762,COMMANDSTR,1,IDX,2)),"^",2)
 QUIT
 ;"
ONEOFFICE
 ;"COMMANDSTR - SPECIDX^OFFICEIDX
 ;"TMGRESULT(1)=NAME^PHONE^FAX^NOTES
 ;"TMGRESULT(2...#)=PHYSIEN^PHYSICIANNAME
 NEW SPECIEN,OFFICEIEN
 SET SPECIEN=+$P(COMMANDSTR,"^",1),OFFICEIEN=+$P(COMMANDSTR,"^",2)
 IF SPECIEN'>0 SET TMGRESULT(0)="-1^NO SPECIALTY IEN SENT" QUIT
 IF OFFICEIEN'>0 SET TMGRESULT(0)="-1^NO OFFICE IEN SENT" QUIT
 NEW NOTE SET NOTE=$P($G(^TMG(22762,SPECIEN,1,OFFICEIEN,3)),"^",1)

 SET TMGRESULT(1)=$G(^TMG(22762,SPECIEN,1,OFFICEIEN,0))_"^"_$P($G(^TMG(22762,SPECIEN,1,OFFICEIEN,2)),"^",1)_"^"_$P($G(^TMG(22762,SPECIEN,1,OFFICEIEN,2)),"^",2)_"^"_NOTE
 NEW PHYSIEN SET PHYSIEN=0
 NEW OUTIDX SET OUTIDX=1
 FOR  SET PHYSIEN=$O(^TMG(22762,SPECIEN,1,OFFICEIEN,1,PHYSIEN)) QUIT:PHYSIEN'>0  DO
 . SET TMGRESULT($I(OUTIDX))=PHYSIEN_"^"_$G(^TMG(22762,SPECIEN,1,OFFICEIEN,1,PHYSIEN,0))
 QUIT
ADDDOC
 ;"COMMANDSTR - SPECIDX^OFFICEIDX^NEWDOCNAME
 NEW SPECIEN,OFFICEIEN,NEWDOCNAME
 SET SPECIEN=+$P(COMMANDSTR,"^",1),OFFICEIEN=+$P(COMMANDSTR,"^",2),NEWDOCNAME=$P(COMMANDSTR,"^",3)
 NEW TMGFDA,TMGERR
 SET TMGFDA(22762.11,"+1,"_OFFICEIEN_","_SPECIEN_",",.01)=NEWDOCNAME
 DO UPDATE^DIE("","TMGFDA","TMGERR") 
 QUIT
 ;"
DELDOC
 ;"COMMANDSTR - SPECIDX^OFFICEIDX^DOCIEN
 NEW SPECIEN,OFFICEIEN,DOCIEN
 SET SPECIEN=+$P(COMMANDSTR,"^",1),OFFICEIEN=+$P(COMMANDSTR,"^",2),DOCIEN=$P(COMMANDSTR,"^",3)
 NEW TMGFDA,TMGERR
 SET TMGFDA(22762.11,DOCIEN_","_OFFICEIEN_","_SPECIEN_",",.01)="@"
 DO FILE^DIE("","TMGFDA","TMGERR") 
 QUIT
 ;"
 SET TMGRESULT="-1^UNKNOWN COMMAND: "_COMMAND
 QUIT
 ;"
SAVEOFFICE
 ;"COMMANDSTR - SPECIDX^OFFICEIDX^NAME,PHONE,FAX,NOTE
 NEW SPECIEN,OFFICEIEN
 SET SPECIEN=+$P(COMMANDSTR,"^",1),OFFICEIEN=+$P(COMMANDSTR,"^",2)
 NEW TMGFDA,TMGERR
 NEW NAME,PHONE,FAX,NOTE
 SET NAME=$P(COMMANDSTR,"^",3),PHONE=$P(COMMANDSTR,"^",4)
 SET FAX=$P(COMMANDSTR,"^",5),NOTE=$P(COMMANDSTR,"^",6)
 SET TMGFDA(22762.01,OFFICEIEN_","_SPECIEN_",",.01)=NAME
 SET TMGFDA(22762.01,OFFICEIEN_","_SPECIEN_",",.02)=PHONE
 SET TMGFDA(22762.01,OFFICEIEN_","_SPECIEN_",",.03)=FAX
 SET TMGFDA(22762.01,OFFICEIEN_","_SPECIEN_",",.04)=NOTE
 DO FILE^DIE("","TMGFDA","TMGERR") 
 QUIT
 ;"
 SET TMGRESULT="-1^UNKNOWN COMMAND: "_COMMAND
 QUIT
 ;"
SETHTML2(ROOT,RESULTS,TITLE,HEADING,COLNUMS,FAXES)  ;
  ;"Input: ROOT -- AN OUT PARAMETER 
  ;"          @ROOT@(1)= HEADING
  ;"          @ROOT@(2)=one long string with HTML codes.
  ;"          @ROOT@(3)=END OF TABLE                
  ;"       RESULTS -- INPUT DATA.  Pass by reference.  Format:  
  ;"            RESULT(#)=<COL1>^<COL2)^<COL3>
  ;"       TITLE -- STRING FOR TITLE OF TABLE
  ;"       HEADING -- Column titles, carot deliminated
  ;"             <Title1>^<Title2>^<Title3>
  ;"       COLNUM -- number of colums
  ;"Results -- none
  NEW END SET END=3
  MERGE ^EDDIE("TMGRPT2")=RESULTS
  NEW DATA
  SET @ROOT@(1)="<HTML><HEAD><TITLE>"_TITLE_"</TITLE></HEAD><BODY>"
  SET DATA=HEADING
  NEW IDX SET IDX=0
  NEW LASTSPECIALTY SET LASTSPECIALTY="" 
  FOR  SET IDX=$ORDER(RESULTS(IDX)) QUIT:IDX'>0  DO
  . NEW LINE SET LINE=$GET(RESULTS(IDX))
  . ;"
  . NEW CLEANLINE SET CLEANLINE=$TR(LINE,"-")  ;"USED TO TEST FAX NUMBERS
  . NEW HLIGHT,FIDX SET HLIGHT=0,FIDX=0
  . FOR  SET FIDX=$O(FAXES(FIDX)) QUIT:FIDX'>0  DO
  . . NEW FNUM SET FNUM=$$TRIM^XLFSTR($G(FAXES(FIDX)))
  . . IF CLEANLINE[FNUM SET HLIGHT=1
  . ;"
  . NEW PIECE
  . FOR PIECE=1:1:COLNUMS  DO
  . . IF PIECE=1 DO
  . . . NEW SPECIALTY SET SPECIALTY=$PIECE(LINE,"^",PIECE)
  . . . IF LASTSPECIALTY=SPECIALTY QUIT
  . . . SET LASTSPECIALTY=SPECIALTY
  . . . SET DATA=DATA_"<TR bgcolor=""#C4E3ED"" align=""center""><TD COLSPAN=""4"">"_SPECIALTY_"</TD></TR>"
  . . ELSE  DO
  . . . IF PIECE=2 DO
  . . . . IF HLIGHT=1 DO
  . . . . . SET DATA=DATA_"<TR bgcolor=""#FFFF00"">"
  . . . . ELSE  DO
  . . . . . SET DATA=DATA_"<TR>"
  . . . SET DATA=DATA_"<TD>"_$PIECE(LINE,"^",PIECE)_"</TD>"
  . SET DATA=DATA_"</TR>"
  . SET END=END+1
  SET @ROOT@(2)=DATA
  SET @ROOT@(3)="</TABLE></BODY></HTML>"
  QUIT          
  ;"
CNSLTANT(TMGRESULT,TMGDFN)  ;"Consultants for Chart Exporter
  ;"Purpose: Consultants - For Chart Exporter 
  NEW CONSULTARR
  NEW OUTIDX SET OUTIDX=0
  DO GTCONSLT(.CONSULTARR)
  ;"
  NEW FAXES DO RECNTFAX^TMGTIUPR(.FAXES,TMGDFN)
  NEW FIDX SET FIDX=0
  FOR  SET FIDX=$O(FAXES(FIDX)) QUIT:FIDX'>0  DO
  . SET FAXES(FIDX)=$TR($P($G(FAXES(FIDX))," = ",2),"-")
  ;"
  NEW SPECIALTY SET SPECIALTY=""
  FOR  SET SPECIALTY=$O(CONSULTARR(SPECIALTY)) QUIT:SPECIALTY=""  DO
  . NEW OFFICE SET OFFICE=""
  . FOR  SET OFFICE=$O(CONSULTARR(SPECIALTY,OFFICE)) QUIT:OFFICE=""  DO
  . . NEW LINE,PHONE,FAX
  . . SET LINE=$G(CONSULTARR(SPECIALTY,OFFICE)),PHONE=$P(LINE,"^",1),FAX=$P(LINE,"^",2)
  . . SET TMGRESULT($I(OUTIDX))=SPECIALTY_"^"_OFFICE_"^"_PHONE_"^"_FAX
  QUIT
  ;"